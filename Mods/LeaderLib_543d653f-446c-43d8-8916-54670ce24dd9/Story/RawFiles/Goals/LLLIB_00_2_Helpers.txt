Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLLIB_Helper_Temp_StopIteratingForFlags(1)
//DB_LLLIB_Helper_Temp_StopIteratingForMinLevel(1)
//DB_LLLIB_Helper_Temp_StopIteratingForMaxLevel(1)
//DB_LLLIB_Helper_Temp_PartyMemberOutsideOfLevelRange(1)
//DB_LLLIB_Helper_Temp_HighestLevelInParty(_Level)
//DB_LLLIB_Helper_Temp_CreateStatQueueArray(_Character, _ArrayName)
//DB_LLLIB_Helper_Temp_CreateStatQueue(_Character, _ArrayName, _Stat, _Equip, _ShowInTrade)
//DB_LLLIB_Helper_Temp_CreateByStatTimer(_Character, _TimerName)
//DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _TimerName)
KBSECTION
//REGION QUERY_INFO
QRY
LLLIB_Helper_QRY_PartyFlag_ClearIterator()
AND
DB_LLLIB_Helper_Temp_StopIteratingForFlags(_Val)
THEN
NOT DB_LLLIB_Helper_Temp_StopIteratingForFlags(_Val);

QRY
LLLIB_Helper_QRY_PartyFlag_ClearIterator()
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_PartyFlagSet((STRING)_Flag, (STRING)_FlagType, (INTEGER)_FlagVal)
AND
LLLIB_Helper_QRY_PartyFlag_ClearIterator()
AND
DB_IsPlayer(_Player)
AND
_Player != NULL_00000000-0000-0000-0000-000000000000
AND
NOT DB_LLLIB_Helper_Temp_StopIteratingForFlags(1)
AND
LLLIB_Helper_QRY_FlagSet(_Player, _Flag, _FlagType, _FlagVal)
THEN
DB_LLLIB_Helper_Temp_StopIteratingForFlags(1);

QRY
LLLIB_Helper_QRY_FlagSet((GUIDSTRING)_Object, (STRING)_Flag, "Object", (INTEGER)_FlagVal)
AND
ObjectGetFlag(_Object, _Flag, _FlagVal)
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_FlagSet((CHARACTERGUID)_Player, (STRING)_Flag, "Character", (INTEGER)_FlagVal)
AND
ObjectGetFlag(_Player, _Flag, _FlagVal)
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_FlagSet((CHARACTERGUID)_Player, (STRING)_Flag, "User", (INTEGER)_FlagVal)
AND
UserGetFlag(_Player, _Flag, _FlagVal)
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_FlagSet((GUIDSTRING)_Object, (STRING)_Flag, "Global", (INTEGER)_FlagVal)
AND
GlobalGetFlag(_Flag, _FlagVal)
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_PartyMinLevelMet((INTEGER)_MinLevel)
AND
_MinLevel <= 0
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_PartyMinLevelMet((INTEGER)_MinLevel)
AND
DB_LLLIB_Helper_Temp_StopIteratingForMinLevel(_Val)
THEN
NOT DB_LLLIB_Helper_Temp_StopIteratingForMinLevel(_Val);

QRY
LLLIB_Helper_QRY_PartyMinLevelMet((INTEGER)_MinLevel)
AND
_MinLevel > 0
AND
DB_IsPlayer(_Player)
AND
NOT DB_LLLIB_Helper_Temp_StopIteratingForMinLevel(1)
AND
CharacterGetLevel(_Player, _Level)
AND
_Level >= _MinLevel
THEN
DB_LLLIB_Helper_Temp_StopIteratingForMinLevel(1);

QRY
LLLIB_Helper_QRY_PartyMaxLevelMet((INTEGER)_MaxLevel)
AND
_MaxLevel <= 0
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_PartyMaxLevelMet((INTEGER)_MaxLevel)
AND
DB_LLLIB_Helper_Temp_StopIteratingForMaxLevel(_Val)
THEN
NOT DB_LLLIB_Helper_Temp_StopIteratingForMaxLevel(_Val);

QRY
LLLIB_Helper_QRY_PartyMaxLevelMet((INTEGER)_MaxLevel)
AND
_MaxLevel > 0
AND
DB_IsPlayer(_Player)
AND
NOT DB_LLLIB_Helper_Temp_StopIteratingForMaxLevel(1)
AND
CharacterGetLevel(_Player, _Level)
AND
_Level <= _MaxLevel
THEN
DB_LLLIB_Helper_Temp_StopIteratingForMaxLevel(1);

QRY
LLLIB_Helper_QRY_PartyLevelRangeMet((INTEGER)_MinLevel, (INTEGER)_MaxLevel)
AND
LLLIB_Helper_QRY_PartyMinLevelMet(_MinLevel)
AND
LLLIB_Helper_QRY_PartyMaxLevelMet(_MaxLevel)
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_GetHighestLevelInParty()
AND
DB_LLLIB_Helper_Temp_HighestLevelInParty(_Level)
THEN
NOT DB_LLLIB_Helper_Temp_HighestLevelInParty(_Level);
DB_LLLIB_Helper_Temp_HighestLevelInParty(1);

QRY
LLLIB_Helper_QRY_GetHighestLevelInParty()
AND
NOT DB_LLLIB_Helper_Temp_HighestLevelInParty(_)
THEN
DB_LLLIB_Helper_Temp_HighestLevelInParty(1);

QRY
LLLIB_Helper_QRY_GetHighestLevelInParty()
AND
DB_IsPlayer(_Player)
AND
DB_LLLIB_Helper_Temp_HighestLevelInParty(_HighestLevel)
AND
CharacterGetLevel(_Player, _Level)
AND
_Level > _HighestLevel
THEN
NOT DB_LLLIB_Helper_Temp_HighestLevelInParty(_HighestLevel);
DB_LLLIB_Helper_Temp_HighestLevelInParty(_Level);

QRY
LLLIB_Helper_QRY_GetHighestLevelCharacterInParty()
AND
DB_LLLIB_Helper_Temp_HighestLevelCharacterInParty(_Player, _Level)
THEN
NOT DB_LLLIB_Helper_Temp_HighestLevelCharacterInParty(_Player, _Level);

QRY
LLLIB_Helper_QRY_GetHighestLevelCharacterInParty()
AND
LLLIB_Helper_QRY_GetHighestLevelInParty()
AND
DB_LLLIB_Helper_Temp_HighestLevelInParty(_Level)
AND
DB_IsPlayer(_Player)
AND
CharacterGetLevel(_Player, _Level)
THEN
NOT DB_LLLIB_Helper_Temp_HighestLevelInParty(_Level);
DB_LLLIB_Helper_Temp_HighestLevelCharacterInParty(_Player, _Level);

QRY
LLLIB_Helper_QRY_UserMatch((CHARACTERGUID)_Char1, (CHARACTERGUID)_Char2)
AND
CharacterGetReservedUserID(_Char1, _ID)
AND
CharacterGetReservedUserID(_Char2, _ID)
THEN
DB_NOOP(1);
//END_REGION

//REGION PARTY_LEVELRANGE_STRICT
QRY
LLLIB_Helper_QRY_PartyMinLevelMet_Strict((INTEGER)_MinLevel)
AND
_MinLevel <= 0
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_PartyMinLevelMet_Strict((INTEGER)_MinLevel)
AND
DB_LLLIB_Helper_Temp_PartyMemberOutsideOfLevelRange(_Val)
THEN
NOT DB_LLLIB_Helper_Temp_PartyMemberOutsideOfLevelRange(_Val);

QRY
LLLIB_Helper_QRY_PartyMinLevelMet_Strict((INTEGER)_MinLevel)
AND
_MinLevel > 0
AND
DB_IsPlayer(_Player)
AND
NOT DB_LLLIB_Helper_Temp_PartyMemberOutsideOfLevelRange(1)
AND
CharacterGetLevel(_Player, _Level)
AND
_Level < _MinLevel
THEN
DB_LLLIB_Helper_Temp_PartyMemberOutsideOfLevelRange(1);

QRY
LLLIB_Helper_QRY_PartyMaxLevelMet_Strict((INTEGER)_MaxLevel)
AND
_MaxLevel <= 0
THEN
DB_NOOP(1);

QRY
LLLIB_Helper_QRY_PartyMaxLevelMet_Strict((INTEGER)_MaxLevel)
AND
DB_LLLIB_Helper_Temp_StopIteratingForMaxLevel(_Val)
THEN
NOT DB_LLLIB_Helper_Temp_StopIteratingForMaxLevel(_Val);

QRY
LLLIB_Helper_QRY_PartyMaxLevelMet_Strict((INTEGER)_MaxLevel)
AND
_MaxLevel > 0
AND
DB_IsPlayer(_Player)
AND
NOT DB_LLLIB_Helper_Temp_PartyMemberOutsideOfLevelRange(1)
AND
CharacterGetLevel(_Player, _Level)
AND
_Level > _MaxLevel
THEN
DB_LLLIB_Helper_Temp_PartyMemberOutsideOfLevelRange(1);

//Checks to make sure the whole party fits the range
QRY
LLLIB_Helper_QRY_PartyLevelRangeMet_Strict((INTEGER)_MinLevel, (INTEGER)_MaxLevel)
AND
LLLIB_Helper_QRY_PartyMinLevelMet_Strict(_MinLevel)
AND
LLLIB_Helper_QRY_PartyMaxLevelMet_Strict(_MaxLevel)
AND
NOT DB_LLLIB_Helper_Temp_PartyMemberOutsideOfLevelRange(1)
THEN
DB_NOOP(1);
//END_REGION

//REGION FLAG_HELPERS
QRY
LLLIB_Helper_QRY_ClearObjectFlag((GUIDSTRING)_Object, (STRING)_Flag)
THEN
ObjectClearFlag(_Object, _Flag);

QRY
LLLIB_Helper_QRY_ClearUserFlag((CHARACTERGUID)_Object, (STRING)_Flag)
THEN
UserClearFlag(_Object, _Flag, 0);

QRY
LLLIB_Helper_QRY_ClearGlobalFlag((STRING)_Flag)
THEN
GlobalClearFlag(_Flag);
//END_REGION

//REGION RANDOM
//Thanks to Norbyte for this chunk
QRY
LLLIB_Random_QRY_AdjustSeed()
AND
NOT DB_LLLIB_Random_Seed(_)
AND
Random(99999, _Seed)
THEN
DB_LLLIB_Random_Seed(_Seed);

QRY
LLLIB_Random_QRY_AdjustSeed()
THEN
DB_NOOP(1);

/*
QRY
LLLIB_Random_QRY_AdjustSeed()
AND
DB_LLLIB_Random_Seed(_Seed)
AND
IntegerProduct(_Seed, 1664525, _SeedLCG1)
AND
IntegerSum(_SeedLCG1, 1013904223, _SeedLCG2)
THEN
NOT DB_LLLIB_Random_Seed(_Seed);
DB_LLLIB_Random_Seed(_SeedLCG2);
*/

QRY
LLLIB_Random((INTEGER)_Modulo)
AND
DB_LLLIB_Temp_RandomResult(_OldRand)
THEN
NOT DB_LLLIB_Temp_RandomResult(_OldRand);

QRY
LLLIB_Random((INTEGER)_Modulo)
AND
LLLIB_Random_QRY_AdjustSeed()
AND
DB_LLLIB_Random_Seed(_Seed)
AND
Random(_Seed, _Ran)
AND
IntegerModulo(_Ran, _Modulo, _Rand)
THEN
DB_LLLIB_Temp_RandomResult(_Rand);

//Random range
QRY
LLLIB_Random((INTEGER)_MinRan, (INTEGER)_MaxRan)
AND
NOT DB_LLLIB_Random_Seed(_)
AND
Random(99999, _Seed)
THEN
DB_LLLIB_Random_Seed(_Seed);

//END_REGION

//REGION RANDOM_RANGE
QRY
LLLIB_Random((INTEGER)_MinRan, (INTEGER)_MaxRan)
AND
DB_LLLIB_Temp_RandomResult(_OldRand)
THEN
NOT DB_LLLIB_Temp_RandomResult(_OldRand);

//min + rand() / (RAND_MAX / (max - min + 1) + 1)
QRY
LLLIB_Random((INTEGER)_MinRan, (INTEGER)_MaxRan)
AND
Random(99999, _RanSeed)
AND
IntegerSubtract(_MaxRan, _MinRan, _Ran1)
AND
IntegerSum(_Ran1, 1, _Ran2)
AND
IntegerDivide(99999, _Ran2, _Ran3)
AND
IntegerSum(_Ran3, 1, _Ran4)
AND
IntegerDivide(_RanSeed, _Ran4, _Ran5)
AND
IntegerSum(_Ran5, _MinRan, _Val)
THEN
DB_LLLIB_Temp_RandomResult(_Val);
//END_REGION

//REGION DATABASE_HELPERS
PROC
LLLIB_Helper_ClearExistingDatabase((STRING)_DatabaseName, (INTEGER)_Arity)
AND
SysCount(_DatabaseName, _Arity, _Count)
AND
_Count > 0
THEN
SysClear(_DatabaseName, _Arity);

PROC
LLLIB_Helper_DatabaseCount((STRING)_DatabaseName, (INTEGER)_Arity)
AND
SysCount(_DatabaseName, _Arity, _Total)
AND
IntegertoString(_Total, _TotalStr)
AND
StringConcatenate("[LLLIB:Helpers(DatabaseCount)] '", _DatabaseName, _Str1)
AND
StringConcatenate(_Str1, "' Total Entries: ", _Str2)
AND
StringConcatenate(_Str2, _TotalStr, _Str3)
THEN
LeaderLog_Log("DEBUG", _Str3);
//END_REGION

//REGION EQUIPMENT_REFRESH
PROC
LLLIB_Helper_RefreshEquipment((CHARACTERGUID)_Character)
AND
DB_LLLIB_EquipmentSlots(_Slot)
AND
CharacterGetEquippedItem(_Character, _Slot, (ITEMGUID)_Item)
THEN
CharacterUnequipItem(_Character, _Item);
CharacterEquipItem(_Character, _Item);

PROC
LLLIB_Helper_UnequipWeapons((CHARACTERGUID)_Character)
AND
CharacterGetEquippedWeapon(_Character, (ITEMGUID)_Item)
THEN
CharacterUnequipItem(_Character, _Item);

PROC
LLLIB_Helper_UnequipWeapons((CHARACTERGUID)_Character)
AND
CharacterGetEquippedShield(_Character, (ITEMGUID)_Item)
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "WPN", 1)
THEN
CharacterUnequipItem(_Character, _Item);
//END_REGION

//REGION ITEM_SPAWNING
PROC
LLLIB_Helper_CreateItemByStat((CHARACTERGUID)_Character, (STRING)_Stat)
THEN
LLLIB_Helper_CreateItemByStat(_Character, _Stat, 0, 1);

PROC
LLLIB_Helper_CreateItemByStat((CHARACTERGUID)_Character, (STRING)_Stat, (INTEGER)_Equip)
THEN
LLLIB_Helper_CreateItemByStat(_Character, _Stat, _Equip, 1);

PROC
LLLIB_Helper_CreateItemByStat((CHARACTERGUID)_Character, (STRING)_Stat, (INTEGER)_Equip, (INTEGER)_ShowInTrade)
AND
NOT DB_LLLIB_Helper_Temp_CreateStatQueue(_Character, _, _Stat, _, _)
AND
GetUUID(_Character, _ID)
AND
StringConcatenate("LLLIB_Helper_CreateByStat_", _ID, _ArrayName)
THEN
DB_LLLIB_Helper_Temp_CreateStatQueueArray(_Character, _ArrayName);
LeaderLog_Log("DEBUG", "[LLLIB:Helpers:CreateItemByStat] Attempting to generate item by stat [",_Stat,"] and equip it when it's added.");
DB_LLLIB_Helper_Temp_CreateStatQueue(_Character, _ArrayName, _Stat, _Equip, _ShowInTrade);
LLLIB_Array_AddToArray(_ArrayName, _Stat);

PROC
LLLIB_Helper_CreateItemByStat_Start((CHARACTERGUID)_Character, (INTEGER)_TickRate)
AND
NOT DB_LLLIB_Helper_Temp_CreateByStatTimer(_Character, _, _)
AND
GetUUID(_Character, _ID)
AND
StringConcatenate("LLLIB_Timers_Helper_CreateByStat_", _ID, _TimerName)
THEN
DB_LLLIB_Helper_Temp_CreateByStatTimer(_Character, _TimerName, _TickRate);
LLLIB_Helper_Internal_CreateNextStatItem(_Character);

QRY
LLLIB_Helper_CreateItemByStat_QRY_JobDone((CHARACTERGUID)_Character)
AND
NOT DB_LLLIB_Helper_Temp_CreateStatQueue(_Character, _, _, _, _)
THEN
DB_NOOP(1);

PROC
LLLIB_Helper_CreateItemByStat_Cleanup((CHARACTERGUID)_Character)
AND
GetUUID(_Character, _ID)
AND
StringConcatenate("LLLIB_Helper_CreateByStat_", _ID, _ArrayName)
THEN
LLLIB_Array_ClearArray(_ArrayName);

PROC
LLLIB_Helper_CreateItemByStat_StartNextTick((CHARACTERGUID)_Character)
AND
NOT LLLIB_Helper_CreateItemByStat_QRY_JobDone(_Character)
AND
DB_LLLIB_Helper_Temp_CreateByStatTimer(_Character, _TimerName, _TickRate)
THEN
TimerLaunch(_TimerName, _TickRate);

IF
TimerFinished(_TimerName)
AND
DB_LLLIB_Helper_Temp_CreateByStatTimer(_Character, _TimerName, _TickRate)
THEN
LLLIB_Helper_Internal_CreateNextStatItem(_Character);

PROC
LLLIB_Helper_Internal_CreateNextStatItem((CHARACTERGUID)_Character)
AND
DB_LLLIB_Helper_Temp_CreateStatQueueArray(_Character, _ArrayName)
AND
DB_LLLIB_Array_Data(_ArrayName, 0, _Stat)
AND
DB_LLLIB_Helper_Temp_CreateStatQueue(_Character, _ArrayName, _Stat, _Equip, _ShowInTrade)
AND
LLLIB_QRY_Treasure_PrepareGeneratorDummy()
AND
DB_LLLIB_Treasure_GeneratorDummies(_Dummy)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Helpers:CreateItemByStat] Creating [",_Stat,"] on dummy.");
DB_LLLIB_Helpers_Temp_DummyTarget(_Character, _Dummy, _Stat);
NOT DB_LLLIB_Array_Data(_ArrayName, 0, _Stat);
LLLIB_Array_ShiftArrayUp(_ArrayName, 0);
SetVarFixedString(_Dummy, "LLLIB_Helpers_NextCreatedEquipmentStat", _Stat);
SetVarInteger(_Dummy, "LLLIB_Helpers_ShowNextItemInTrade", _ShowInTrade);
LLLIB_Helper_Internal_StartTimeoutTimer(_Character);
SetStoryEvent(_Dummy, "LLLIB_Events_CreateEquipmentByStat");
NOT DB_LLLIB_Helper_Temp_CreateStatQueue(_Character, _ArrayName, _Stat, _Equip, _ShowInTrade);

IF
ItemAddedToCharacter(_Item, _Dummy)
AND
ObjectGetFlag(_Dummy, "LLLIB_GeneratorDummy_CreatedEquipmentItem", 1)
AND
DB_LLLIB_Helpers_Temp_DummyTarget(_Character, _Dummy, _Stat)
AND
GetTemplate(_Item, _ItemTemplate)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Helpers:CreateItemByStat] Equipping [",_ItemTemplate,"]");
LLLIB_Helper_Internal_CancelTimeoutTimer(_Character);
NOT DB_LLLIB_Helpers_Temp_DummyTarget(_Character, _Dummy, _Stat);
SetVarFixedString(_Item, "LLLIB_GeneratedItemStat", _Stat);
DB_LLLIB_Treasure_StatToTemplate(_Stat, _ItemTemplate);
ObjectClearFlag(_Dummy, "LLLIB_GeneratorDummy_CreatedEquipmentItem");
LLLIB_Helper_CreateItemByStat_StartNextTick(_Character);
ItemToInventory(_Item, _Character, 1, 0, 1);
CharacterEquipItem(_Character, _Item);
LLLIB_Helper_CreateItemByStat_StartNextTick(_Character);

PROC
LLLIB_Helper_Internal_CancelTimeoutTimer((CHARACTERGUID)_Character)
AND
DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _TimerName)
THEN
TimerCancel(_TimerName);

PROC
LLLIB_Helper_Internal_StartTimeoutTimer((CHARACTERGUID)_Character)
AND
NOT DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _)
AND
GetUUID(_Character, _ID)
AND
StringConcatenate("LLLIB_Timers_Helper_CreateByStat_Timeout_", _ID, _TimerName)
THEN
DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _TimerName);

PROC
LLLIB_Helper_Internal_StartTimeoutTimer((CHARACTERGUID)_Character)
AND
DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _TimerName)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, 500);

IF
TimerFinished(_TimerName)
AND
DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _TimerName)
THEN
NOT DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _TimerName);
LeaderLog_Log("DEBUG", "[LLLIB:Helpers:CreateItemByStat] Failed to create item (timeout timer finished). Starting next tick.");
LLLIB_Helper_CreateItemByStat_StartNextTick(_Character);

PROC
LLLIB_Helper_CreateItemByStat_StartNextTick((CHARACTERGUID)_Character)
AND
LLLIB_Helper_CreateItemByStat_QRY_JobDone(_Character)
AND
DB_LLLIB_Helper_Temp_CreateStatQueueArray(_Character, _ArrayName)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Helpers:CreateItemByStat] Job finished. Cleaning up databases.");
LLLIB_Array_ClearArray(_ArrayName);
NOT DB_LLLIB_Helper_Temp_CreateStatQueueArray(_Character, _ArrayName);
LLLIB_Helper_CreateItemByStat_Cleanup(_Character);


PROC
LLLIB_Helper_CreateItemByStat_Cleanup((CHARACTERGUID)_Character)
AND
DB_LLLIB_Helper_Temp_CreateStatQueue(_Character, _ArrayName, _Stat, _Equip, _ShowInTrade)
THEN
NOT DB_LLLIB_Helper_Temp_CreateStatQueue(_Character, _ArrayName, _Stat, _Equip, _ShowInTrade);

PROC
LLLIB_Helper_CreateItemByStat_Cleanup((CHARACTERGUID)_Character)
AND
DB_LLLIB_Helper_Temp_CreateByStatTimer(_Character, _TimerName)
THEN
TimerCancel(_TimerName);
NOT DB_LLLIB_Helper_Temp_CreateByStatTimer(_Character, _TimerName);

PROC
LLLIB_Helper_CreateItemByStat_Cleanup((CHARACTERGUID)_Character)
AND
DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _TimerName)
THEN
TimerCancel(_TimerName);
NOT DB_LLLIB_Helper_Temp_CreateByStatTimeoutTimer(_Character, _TimerName);
//END_REGION

//REGION HOST_FLAGGING
IF
StoryEvent(_,"LeaderLib_Initialized")
THEN
//TimerLaunch("LLLIB_Timers_CheckHost", 1);
LLLIB_Helper_MarkHost();

IF
TimerFinished("LLLIB_Timers_CheckHost")
THEN
LLLIB_Helper_MarkHost();

PROC
LLLIB_Helper_MarkHost()
AND
CharacterGetHostCharacter(_Host)
AND
UserGetFlag(_Host, "LeaderLib_IsHost", 0)
THEN
LeaderLog_Log("DEBUG","[LLLIB:Helpers] Flagged host user.");
UserSetFlag(_Host, "LeaderLib_IsHost", 0);

PROC
LLLIB_Helper_MarkHost()
AND
CharacterGetHostCharacter(_Host)
AND
DB_IsPlayer(_Player)
AND
_Player != _Host
AND
NOT LLLIB_Helper_QRY_UserMatch(_Player, _Host)
AND
UserGetFlag(_Player, "LeaderLib_IsHost", 1)
THEN
LeaderLog_Log("DEBUG","[LLLIB:Helpers] Cleared host flag from non-host user.");
UserClearFlag(_Player, "LeaderLib_IsHost", 0);

PROC
LLLIB_Helper_MarkHost()
AND
NOT CharacterGetHostCharacter(_)
THEN
LeaderLog_Log("DEBUG","[LLLIB:Helpers][ERROR] No host character found.");
//END_REGION

//REGION EDITOR_FLAGGING
IF
GameStarted(_, 1)
THEN
GlobalSetFlag("LeaderLib_IsEditorMode");

IF
GameStarted(_, 0)
THEN
GlobalClearFlag("LeaderLib_IsEditorMode");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
