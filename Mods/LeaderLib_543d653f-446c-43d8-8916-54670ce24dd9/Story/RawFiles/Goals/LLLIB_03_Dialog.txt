Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_Dialog_InitSettings();

//DB_LLLIB_DialogNames(_Dialog)
//DB_LLLIB_Dialog_AttitudeFlags(_Flag, _Amount)
//DB_LLLIB_Dialog_NameOverride(_Character, _Name)
KBSECTION
//REGION SETTINGS
PROC
LLLIB_Dialog_InitSettings()
THEN
LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_DialogNames", 1);
DB_LLLIB_DialogNames("LLLIB_ModMenu");
DB_LLLIB_DialogNames("LLLIB_LeaderTrader_Default");
DB_LLLIB_DialogNames("LLLIB_LeaderTrader_FortJoy_Intro");
DB_LLLIB_DialogNames("LLLIB_LeaderTrader_LadyVengeance_Intro");
DB_LLLIB_DialogNames("LLLIB_LeaderTrader_Tutorial_Intro");
DB_LLLIB_DialogNames("LLLIB_LeaderTrader_AttackResponse");
DB_LLLIB_DialogNames("LLLIB_SettingsMenu");

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_Dialog_AttitudeFlags", 2);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_IncreaseAttitude_5", 5);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_IncreaseAttitude_10", 10);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_IncreaseAttitude_25", 25);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_IncreaseAttitude_50", 50);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_IncreaseAttitude_75", 75);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_IncreaseAttitude_100", 100);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_DecreaseAttitude_5", -5);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_DecreaseAttitude_10", -10);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_DecreaseAttitude_25", -25);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_DecreaseAttitude_50", -50);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_DecreaseAttitude_75", -75);
DB_LLLIB_Dialog_AttitudeFlags("LeaderLib_DecreaseAttitude_100", -100);

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_DialogVars", 2);
DB_LLLIB_DialogVars("ModMenu_PageInfo", "LLLIB_ModMenu_PageInfo_2b744313-9239-412c-80a8-f3b5e8b56156");
DB_LLLIB_DialogVars("Settings_ModList", "LLLIB_Settings_ModList_50c55fbe-ad1e-490e-92d1-8836ca42ca12");
DB_LLLIB_DialogVars("SpeakerName", "LLLIB_SpeakerName_9fc648d4-7984-44e7-afa4-b8a07f21185b");
DB_LLLIB_DialogVars("PlayerName", "LLLIB_PlayerName_6c5b9559-53e3-47f7-8881-309a930e4be3");
DB_LLLIB_DialogVars("LeaderTrader_Greeting", "LLLIB_LeaderTrader_Greeting_f143b90b-9afc-477e-b133-6a1574341261");
//END_REGION

//REGION UPDATER
IF
StoryEvent(_,"LeaderLib_Commands_UpdateSettings")
THEN
LLLIB_Dialog_InitSettings();
LeaderLog_Log("SYSTEM", "[LLLIB:Dialog] Databases updated.");

PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Dialog", "DB_LLLIB_DialogNames", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Dialog", "DB_LLLIB_Dialog_AttitudeFlags", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "Dialog", "DB_LLLIB_Dialog_NameOverride", 2);
LeaderLog_Log("SYSTEM", "[LLLIB:Dialog] Databases registered.");
//END_REGION

//REGION NAME_VARS
PROC
LLLIB_Dialog_SetNameOverride((CHARACTERGUID)_Character, (STRING)_Name)
AND
DB_LLLIB_Dialog_NameOverride(_Character, (STRING)_PastName)
AND
_Name != _PastName
THEN
NOT DB_LLLIB_Dialog_NameOverride(_Character, _PastName);

PROC
LLLIB_Dialog_SetNameOverride((CHARACTERGUID)_Character, (STRING)_Name)
THEN
DB_LLLIB_Dialog_NameOverride(_Character, _Name);

IF
DialogStarted(_Dialog, _Instance)
AND
DB_LLLIB_DialogNames(_Dialog)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_NPC)
THEN
LLIB_Dialog_SetPlayerName(_Player, _Instance);
LLIB_Dialog_SetNPCName(_NPC, _Instance);

PROC
LLIB_Dialog_SetPlayerName((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLLIB_Dialog_NameOverride(_Player, _)
AND
GetVarString(_Player, "LLNWIZ_CustomName", _CustomName)
AND
DB_LLLIB_Settings_DialogVariables("PlayerName", _PlayerVar)
THEN
DialogSetVariableStringForInstance(_Instance, _PlayerVar, _CustomName);

PROC
LLIB_Dialog_SetPlayerName((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLLIB_Dialog_NameOverride(_Player, _)
AND
NOT GetVarString(_Player, "LLNWIZ_CustomName", _)
AND
CharacterGetDisplayName(_Player, _, _PlayerName)
AND
DB_LLLIB_Settings_DialogVariables("PlayerName", _PlayerVar)
THEN
DialogSetVariableStringForInstance(_Instance, _PlayerVar, _PlayerName);

PROC
LLIB_Dialog_SetPlayerName((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_Dialog_NameOverride(_Player, _Name)
AND
DB_LLLIB_Settings_DialogVariables("PlayerName", _PlayerVar)
THEN
DialogSetVariableStringForInstance(_Instance, _PlayerVar, _Name);

PROC
LLIB_Dialog_SetNPCName((CHARACTERGUID)_Character, (INTEGER)_Instance)
AND
NOT DB_LLLIB_Dialog_NameOverride(_Character, _)
AND
GetVarString(_Character, "LLNWIZ_CustomName", _CustomName)
AND
DB_LLLIB_Settings_DialogVariables("SpeakerName", _SpeakerVar)
THEN
DialogSetVariableStringForInstance(_Instance, _SpeakerVar, _CustomName);

PROC
LLIB_Dialog_SetNPCName((CHARACTERGUID)_Character, (INTEGER)_Instance)
AND
NOT DB_LLLIB_Dialog_NameOverride(_Character, _)
AND
NOT GetVarString(_Character, "LLNWIZ_CustomName", _)
AND
CharacterGetDisplayName(_Character, _, _SpeakerName)
AND
DB_LLLIB_Settings_DialogVariables("SpeakerName", _SpeakerVar)
THEN
DialogSetVariableStringForInstance(_Instance, _SpeakerVar, _SpeakerName);

PROC
LLIB_Dialog_SetNPCName((CHARACTERGUID)_Character, (INTEGER)_Instance)
AND
DB_LLLIB_Dialog_NameOverride(_Character, _Name)
AND
DB_LLLIB_Settings_DialogVariables("SpeakerName", _SpeakerVar)
THEN
DialogSetVariableStringForInstance(_Instance, _SpeakerVar, _Name);
//END_REGION

//REGION FLAG_COMMANDS
IF
ObjectFlagSet("LeaderLib_GrantModMenuBook", _Object, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Object, "LeaderLib_GrantModMenuBook")
AND
DB_LLLIB_Settings_ItemTemplates("Book_ModMenu", _ItemTemplate)
THEN
ItemTemplateAddTo(_ItemTemplate, _Object, 1);

//Attitude
IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Character, _Instance)
AND
DB_LLLIB_Dialog_AttitudeFlags(_Flag, _Amount)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_NPC)
THEN
DB_LLLIB_Dialog_AttitudeStatusText(_NPC, _Amount);
CharacterAddAttitudeTowardsPlayer(_NPC, _Character, _Amount);
ObjectClearFlag(_Character, _Flag);

PROC
DB_LLLIB_Dialog_AttitudeStatusText((CHARACTERGUID)_Target, (INTEGER)_Amount)
AND
_Amount > 0
AND
IntegertoString(_Amount, _AmountStr)
AND
StringConcatenate("<font color='#2EFE2E'>+", _AmountStr, _Str1)
AND
StringConcatenate(_Str1, " Attitude</font>", _StatusText)
THEN
CharacterStatusText(_Target, _StatusText);

PROC
DB_LLLIB_Dialog_AttitudeStatusText((CHARACTERGUID)_Target, (INTEGER)_Amount)
AND
_Amount < 0
AND
IntegertoString(_Amount, _AmountStr)
AND
StringConcatenate("<font color='#FF0000'>-", _AmountStr, _Str1)
AND
StringConcatenate(_Str1, " Attitude</font>", _StatusText)
THEN
CharacterStatusText(_Target, _StatusText);

IF
ObjectFlagSet("LeaderLib_OpenTrade", (CHARACTERGUID)_Object, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Object, "LeaderLib_OpenTrade")
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
StartTrade(_Player, _Object);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
