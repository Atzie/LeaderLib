Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION UPDATER
PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModAPI", "DB_LeaderLib_Initialized", 1);
LeaderLog_Log("SYSTEM", "[LLLIB:System] Databases registered.");
//END_REGION

//REGION EDITOR_FLAGGING
IF
GameStarted(_, 1)
THEN
GlobalSetFlag("LeaderLib_IsEditorMode");

IF
GameStarted(_, 0)
THEN
GlobalClearFlag("LeaderLib_IsEditorMode");
//END_REGION

//REGION INIT_EVENT
IF
GameStarted(_,_)
AND
DB_LeaderLib_Initialized(_Val)
THEN
NOT DB_LeaderLib_Initialized(_Val);

IF
GameStarted(_,_)
THEN
TimerLaunch("LLLIB_Timers_LeaderLibInitialized", 1);

IF
TimerFinished("LLLIB_Timers_LeaderLibInitialized")
THEN
DB_LeaderLib_Initialized(1);
LeaderLog_Log("DEBUG", "[LLLIB:ModAPI] LeaderLib initialized.");
LeaderLib_SendEvent("LeaderLib_Initialized");
//END_REGION

//REGION HOST_FLAGGING
IF
StoryEvent(_,"LeaderLib_Initialized")
THEN
LLLIB_System_MarkHost();

PROC
LLLIB_System_MarkHost()
AND
CharacterGetHostCharacter(_Host)
AND
UserGetFlag(_Host, "LeaderLib_IsHost", 0)
THEN
LeaderLog_Log("DEBUG","[LLLIB:System] Flagged host user.");
UserSetFlag(_Host, "LeaderLib_IsHost", 0);

PROC
LLLIB_System_MarkHost()
AND
CharacterGetHostCharacter(_Host)
AND
DB_IsPlayer(_Player)
AND
_Player != _Host
AND
NOT LLLIB_Helper_QRY_UserMatch(_Player, _Host)
AND
UserGetFlag(_Player, "LeaderLib_IsHost", 1)
THEN
LeaderLog_Log("DEBUG","[LLLIB:System] Cleared host flag from non-host user.");
UserClearFlag(_Player, "LeaderLib_IsHost", 0);

PROC
LLLIB_System_MarkHost()
AND
NOT CharacterGetHostCharacter(_)
THEN
LeaderLog_Log("DEBUG","[LLLIB:System][ERROR] No host character found.");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
