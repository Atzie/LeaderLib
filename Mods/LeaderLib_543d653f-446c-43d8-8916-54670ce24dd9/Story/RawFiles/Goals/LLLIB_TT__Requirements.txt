Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLLIB_Requirement_PartyLevel(_RequirementID, _MinLevel, _MaxLevel)
//DB_LLLIB_Requirement_Flag(_RequirementID, _Flag, _FlagType, _FlagVal)
//DB_LLLIB_Requirement_MaxItemAmount(_RequirementID, _MaxAmount)
KBSECTION
//REGION REGISTERING
PROC
LLLIB_Requirements_AddFlagRequirement((STRING)_RequirementID, (STRING)_Flag, (STRING)_FlagType)
AND
_RequirementID != ""
THEN
LLLIB_Requirements_AddFlagRequirement(_RequirementID, _Flag, _FlagType, 1);

PROC
LLLIB_Requirements_AddFlagRequirement((STRING)_RequirementID, (STRING)_Flag, (STRING)_FlagType, (INTEGER)_FlagVal)
AND
_FlagType != "Character"
AND
_RequirementID != ""
THEN
DB_LLLIB_Requirement_Flag(_RequirementID, _Flag, _FlagType, _FlagVal);

PROC
LLLIB_Requirements_AddFlagRequirement((STRING)_RequirementID, (STRING)_Flag, "Character", (INTEGER)_FlagVal)
AND
_RequirementID != ""
THEN
DB_LLLIB_Requirement_Flag(_RequirementID, _Flag, "Object", _FlagVal);

PROC
LLLIB_Requirements_AddPartyLevelRequirement((STRING)_RequirementID, (INTEGER)_MinLevel, (INTEGER)_MaxLevel)
AND
_RequirementID != ""
THEN
DB_LLLIB_Requirement_PartyLevel(_RequirementID, _MinLevel, _MaxLevel);

PROC
LLLIB_Requirements_AddMaxAmountRequirement((STRING)_RequirementID, (INTEGER)_MaxAmount)
AND
_RequirementID != ""
THEN
DB_LLLIB_Requirement_MaxItemAmount(_RequirementID, _MaxAmount);

//END_REGION

//REGION REQUIREMENT_CHECK
QRY
LLLIB_QRY_Requirements_PartyLevelMet((STRING)_RequirementID)
AND
DB_LLLIB_Requirement_PartyLevel(_RequirementID, _MinLevel, _MaxLevel)
AND
LLLIB_Helper_QRY_PartyLevelRangeMet(_MinLevel, _MaxLevel)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Requirements_PartyLevelMet((STRING)_RequirementID)
AND
NOT DB_LLLIB_Requirement_PartyLevel(_RequirementID, _, _)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Requirements_PartyLevelMet((STRING)_RequirementID)
AND
_RequirementID == ""
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Requirements_PartyFlagMet((STRING)_RequirementID)
AND
DB_LLLIB_Requirement_Flag(_RequirementID, _Flag, _FlagType, _FlagVal)
AND
LLLIB_Helper_QRY_PartyFlagSet(_Flag, _FlagType, _FlagVal)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Requirements_PartyFlagMet((STRING)_RequirementID)
AND
NOT DB_LLLIB_Requirement_Flag(_RequirementID,_,_,_)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Requirements_PartyFlagMet((STRING)_RequirementID)
AND
_RequirementID == ""
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Requirements_AllMet((STRING)_RequirementID)
AND
_RequirementID == ""
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Requirements_AllMet((STRING)_RequirementID)
AND
_RequirementID != ""
AND
LLLIB_QRY_Requirements_PartyFlagMet(_RequirementID)
AND
LLLIB_QRY_Requirements_PartyLevelMet(_RequirementID)
THEN
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLLIB_TreasureAndTraders"
