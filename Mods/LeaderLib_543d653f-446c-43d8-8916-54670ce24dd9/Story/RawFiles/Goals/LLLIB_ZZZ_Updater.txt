Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_Updater_RegisterMod("LeaderLib", "0.1.0.0", "LaughingLeader");
KBSECTION
//REGION MOD_UPDATING
PROC
LLLIB_Updater_ClearPreviousVersions((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_RegisteredMod(_ModName, _Version, _Author)
THEN
NOT DB_Mods_RegisteredMod(_ModName, _Version, _Author);

PROC
LLLIB_Updater_RegisterMod((STRING)_ModName, (STRING)_Version, (STRING)_Author)
AND
NOT DB_Mods_RegisteredMod(_ModName, _Version, _Author)
THEN
LLLIB_Updater_ClearPreviousVersions(_ModName, _Author);
DB_Mods_RegisteredMod(_ModName, _Version, _Author);

QRY
LLLIB_QRY_Updater_ModUpdateNeeded((STRING)_ModName, (STRING)_Version, (STRING)_Author)
AND
NOT DB_Mods_RegisteredMod(_ModName, _Version, _Author)
THEN
DB_NOOP(1);

PROC
LLLIB_System_UpdateDatabases()
THEN
LeaderLog_Debug("[LLLIB:Updater] Databases updated.");

PROC
LLLIB_System_ClearDatabases()
THEN
LeaderLog_Debug("[LLLIB:Updater] Databases cleared.");

PROC
LLLIB_Updater_ResetDatabases()
THEN
LLLIB_System_ClearDatabases();
LLLIB_System_UpdateDatabases();
LeaderLog_Debug("[LLLIB:Updater] Databases cleared and re-initialized.");
//END_REGION

//REGION UPDATE_EVENT
IF
GameStarted(_,_)
AND
LLLIB_QRY_Updater_ModUpdateNeeded("LeaderLib", "0.1.0.0", "LaughingLeader")
THEN
LLLIB_Updater_RegisterMod("LeaderLib", "0.1.0.0", "LaughingLeader");
LLLIB_System_UpdateDatabases();
//END_REGION

//REGION UNINSTALLING
PROC
LLLIB_Updater_UnregisterMod((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_RegisteredMod(_ModName, _Version, _Author)
THEN
NOT DB_Mods_RegisteredMod(_ModName, _Version, _Author);

PROC
LLLIB_Updater_UnregisterMod()
THEN
GlobalClearFlag("LEADERLIB_ACTIVE");

PROC
LLLIB_Updater_UninstallMod()
THEN
LeaderLog_Debug("[LLLIB:Updater:Uninstaller] Uninstalling LeaderLib.");
LeaderLog_Debug("[LLLIB:Updater:Uninstaller] Attempting to end all LeaderLib dialogs. Resuming in 2 seconds.");
LLLIB_Updater_CloseDialogs();
TimerLaunch("LLLIB_Timers_Updater_UninstallerPhase2", 2000);

IF
TimerFinished("LLLIB_Timers_Updater_UninstallerPhase2")
THEN
LeaderLog_Debug("[LLLIB:Updater:Uninstaller] Clearing all flags.");
LLLIB_Updater_ClearFlags();
LeaderLog_Debug("[LLLIB:Updater:Uninstaller] Destroying all LeaderLib items.");
LLLIB_Updater_DestroyItems();
LeaderLog_Debug("[LLLIB:Updater:Uninstaller] Clearing databases.");
LLLIB_System_ClearDatabases();
LeaderLog_Debug("[LLLIB:Updater:Uninstaller] Unregistering LeaderLib.");
LLLIB_Updater_UnregisterMod();

PROC
LLLIB_Updater_CloseDialogs()
THEN
DB_NOOP(1);

/*
PROC
LLLIB_Updater_CloseDialogs()
AND
DB_LLLIB_ActiveDialogs(_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
DialogRequestStop(_Player);
*/

PROC
LLLIB_Updater_ClearFlags()
THEN
DB_NOOP(1);

PROC
LLLIB_Updater_ClearFlags()
AND
DB_IsPlayer(_Player)
THEN
DB_NOOP(1);

PROC
LLLIB_Updater_UnequipItem((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
ItemIsEquipable(_Item, 1)
THEN
CharacterUnequipItem(_Player, _Item);

PROC
LLLIB_Updater_DestroyItems()
THEN
DB_NOOP(1);

/*
PROC
LLLIB_Updater_DestroyItems()
AND
DB_IsPlayer(_Player)
AND
DB_LLLIB_ItemTemplates(_Name, _Template)
AND
GetItemForItemTemplateInInventory(_Player, _Template, _Item)
THEN
LLLIB_Updater_UnequipItem(_Player, _Item);
ItemDestroy(_Item);
*/
//END_REGION

//REGION ACTIVE_FLAG
PROC
LLLIB_Updater_RegisterMod("LeaderLib", (STRING)_Version, "LaughingLeader")
AND
GlobalGetFlag("LEADERLIB_ACTIVE", 0)
THEN
GlobalSetFlag("LEADERLIB_ACTIVE");
//END_REGION
EXITSECTION
LLLIB_Updater_UnregisterMod("LeaderLib", "LaughingLeader");
ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
