Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_ModMenu_InitSettings();

//Settings
//DB_LLLIB_Settings_DialogVariables(_VariableName, _VariableValue)
//DB_LLLIB_ModMenu_MenuVars(_MenuIndex, _DialogVar, _SelectFlag, _DisabledFlag)
//DB_LLLIB_ModMenu_MenusPerPage(_MaxPerPage)
//DB_LLLIB_ModMenu_SortTickRate(_Rate)
//DB_LLLIB_ModMenu_Temp_SortStarted(_Val)

//Dynamic
//DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _ID, _DisplayName)
//DB_LLLIB_ModMenu_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
//DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
//DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
//DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _ID, _Dialog)
//DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance)
KBSECTION
//REGION MENU_REGISTERING
PROC
LeaderLib_RegisterModMenu((STRING)_ID, (STRING)_DisplayName, (STRING)_Dialog, (STRING)_ModName, (STRING)_Author)
AND
NOT DB_LLLIB_ModMenu_RegisteredMenuData(_ID,_,_,_,_)
THEN
LLLIB_StringExt_DetermineStringLength(_DisplayName);
DB_LLLIB_ModMenu_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author);
LLLIB_ModMenu_Internal_StartSortIfNeeded();
//END_REGION

//REGION PAGE_MAX_CALCULATION
IF
TimerFinished("LLLIB_Timers_ModMenu_CalculateMaxPage")
THEN
LLLIB_ModMenu_CalculateMaxPage();

PROC
LLLIB_ModMenu_CalculateMaxPage()
THEN
LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_ModMenu_PageMax", 3);

PROC
LLLIB_ModMenu_CalculateMaxPage()
AND
SysCount("DB_LLLIB_ModMenu_RegisteredMenuData", 5, _TotalMenus)
AND
DB_LLLIB_ModMenu_MenusPerPage(_MaxPerPage)
AND
IntegerSubtract(_MaxPerPage, 1, _Variance)
AND
IntegerSum(_TotalMenus, _Variance, _PageNumToDivide)
AND
IntegerDivide(_PageNumToDivide, _MaxPerPage, _MaxPage)
AND
IntegerSubtract(_TotalMenus, 1, _LastIndex)
AND
IntegerSubtract(_MaxPage, 1, _LastPageIndex)
THEN
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage);

/*
IF
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
IntegertoString(_LastIndex, _Str1)
AND
IntegertoString(_LastPageIndex, _Str2)
AND
IntegertoString(_MaxPage, _Str3)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu] Max page set: LastIndex[",_Str1,"] LastPageIndex[",_Str2,"] MaxPage[",_Str3,"]");
*/
//END_REGION

//REGION SETTINGS
PROC
LLLIB_ModMenu_InitSettings()
THEN
LLLIB_Updater_SaveSettingsVersionData("LLLIB_Settings_ModMenu");

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_ModMenu_SortTickRate", 1);
DB_LLLIB_ModMenu_SortTickRate(1);

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_ModMenu_MenusPerPage", 1);
DB_LLLIB_ModMenu_MenusPerPage(8);

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_ModMenu_MenuVars", 4);
DB_LLLIB_ModMenu_MenuVars(0, "LLLIB_ModMenu_Menu1_783ec14d-2861-4345-8dbc-45974fa34e15", "LLLIB_ModMenu_SelectedMenu1", "LLLIB_ModMenu_MenuEmpty1");
DB_LLLIB_ModMenu_MenuVars(1, "LLLIB_ModMenu_Menu2_9a2d2990-5cff-420e-ba8a-37f9510ab9e1", "LLLIB_ModMenu_SelectedMenu2", "LLLIB_ModMenu_MenuEmpty2");
DB_LLLIB_ModMenu_MenuVars(2, "LLLIB_ModMenu_Menu3_cf00a8c6-02d2-4fda-a766-4f533ac93b2d", "LLLIB_ModMenu_SelectedMenu3", "LLLIB_ModMenu_MenuEmpty3");
DB_LLLIB_ModMenu_MenuVars(3, "LLLIB_ModMenu_Menu4_337a1066-9a85-4005-a2dc-f0156bf13d16", "LLLIB_ModMenu_SelectedMenu4", "LLLIB_ModMenu_MenuEmpty4");
DB_LLLIB_ModMenu_MenuVars(4, "LLLIB_ModMenu_Menu5_45148ce6-e8f3-4af4-81ed-a3b2e1ff8a17", "LLLIB_ModMenu_SelectedMenu5", "LLLIB_ModMenu_MenuEmpty5");
DB_LLLIB_ModMenu_MenuVars(5, "LLLIB_ModMenu_Menu6_863af621-ff0c-454a-b6a3-7c9d3166a8a0", "LLLIB_ModMenu_SelectedMenu6", "LLLIB_ModMenu_MenuEmpty6");
DB_LLLIB_ModMenu_MenuVars(6, "LLLIB_ModMenu_Menu7_888ce02f-83ff-4173-be8d-f41fd348358f", "LLLIB_ModMenu_SelectedMenu7", "LLLIB_ModMenu_MenuEmpty7");
DB_LLLIB_ModMenu_MenuVars(7, "LLLIB_ModMenu_Menu8_a6aa98e6-cde4-491b-a2cb-2cd7d5d71f96", "LLLIB_ModMenu_SelectedMenu8", "LLLIB_ModMenu_MenuEmpty8");
//END_REGION

//REGION SORTING
PROC
LLLIB_ModMenu_Internal_StartSortIfNeeded()
AND
NOT DB_LLLIB_ModMenu_Temp_SortStarted(_)
AND
DB_LeaderLib_Initialized(1)
AND
GlobalGetFlag("LeaderLib_DefaultEventFlowRunning", 0)
AND
LLLIB_ModMenu_QRY_SortNeeded()
THEN
TimerCancel("LLLIB_ModMenu_StartManualSortTimer");
TimerLaunch("LLLIB_ModMenu_StartManualSortTimer", 500);

IF
StoryEvent(_, "LeaderLib_Commands_SortMenus")
THEN
TimerCancel("LLLIB_ModMenu_StartManualSortTimer");

IF
TimerFinished("LLLIB_ModMenu_StartManualSortTimer")
THEN
LLLIB_ModMenu_StartSorting();

QRY
LLLIB_ModMenu_QRY_SortNeeded()
AND
DB_LLLIB_ModMenu_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
THEN
DB_NOOP(1);

//Sorting Start
IF
StoryEvent(_, "LeaderLib_Commands_SortMenus")
AND
LLLIB_ModMenu_QRY_SortNeeded()
THEN
LLLIB_ModMenu_StartSorting();

IF
DB_LLLIB_EventFlow_StopEvent("LeaderLib_Commands_SortMenus")
THEN
LLLIB_DictionarySort_StopSorting("LLLIB_RegisteredMenus");
NOT DB_LLLIB_EventFlow_StopEvent("LeaderLib_Commands_SortMenus");

PROC
LLLIB_ModMenu_SetSortTickRate((INTEGER)_TickRate)
AND
NOT DB_LLLIB_ModMenu_SortTickRate(_)
THEN
DB_LLLIB_ModMenu_SortTickRate(_TickRate);

PROC
LLLIB_ModMenu_SetSortTickRate((INTEGER)_TickRate)
AND
DB_LLLIB_ModMenu_SortTickRate(_OtherTickRate)
AND
_TickRate != _OtherTickRate
THEN
NOT DB_LLLIB_ModMenu_SortTickRate(_OtherTickRate);
DB_LLLIB_ModMenu_SortTickRate(_TickRate);

PROC
LLLIB_ModMenu_StartSorting()
AND
DB_LLLIB_ModMenu_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
AND
NOT DB_LLLIB_DictionarySort_Temp_StringQueue("LLLIB_RegisteredMenus", _ID, _DisplayName)
THEN
LLLIB_DictionarySort_AddString("LLLIB_RegisteredMenus", _ID, _DisplayName);

PROC
LLLIB_ModMenu_StartSorting()
AND
SysCount("DB_LLLIB_ModMenu_RegisteredMenuData", 5, _TotalMenus)
AND
_TotalMenus < 20
AND
IntegertoString(_TotalMenus, _TotalStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu:Sorting] Total menus = [",_TotalStr,"]. Setting sort tick rate to 0.");
LLLIB_ModMenu_SetSortTickRate(0);

PROC
LLLIB_ModMenu_StartSorting()
AND
SysCount("DB_LLLIB_ModMenu_RegisteredMenuData", 5, _TotalMenus)
AND
_TotalMenus >= 20
AND
IntegertoString(_TotalMenus, _TotalStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu:Sorting] Total menus = [",_TotalStr,"]. Setting sort tick rate to 5.");
LLLIB_ModMenu_SetSortTickRate(5);

PROC
LLLIB_ModMenu_StartSorting()
AND
DB_LLLIB_ModMenu_SortTickRate(_TickRate)
THEN
DB_LLLIB_ModMenu_Temp_SortStarted(1);
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu:Sorting] Starting menu sorting.");
LLLIB_DictionarySort_StartSorting("LLLIB_RegisteredMenus", _TickRate, "LeaderLib_Events_OnMenuSortComplete");

IF
StoryEvent(_, "LeaderLib_Events_OnMenuSortComplete")
THEN
NOT DB_LLLIB_ModMenu_Temp_SortStarted(1);
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu:SortingComplete] Menu sorting done.");
TimerCancel("LLLIB_Timers_ModMenu_CalculateMaxPage");
TimerLaunch("LLLIB_Timers_ModMenu_CalculateMaxPage", 1000);
//END_REGION

//REGION CLEAR
PROC
LLLIB_ModMenu_ResetPlayerPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
THEN
NOT DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page);

PROC
LLLIB_ModMenu_ResetPlayerPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _)
THEN
LLLIB_ModMenu_SetPage(_Player, 0, _Instance);
//END_REGION

//REGION PAGE_TURNING
PROC
LLLIB_ModMenu_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _CurrentPage)
THEN
NOT DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _CurrentPage);

PROC
LLLIB_ModMenu_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
THEN
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page);
ObjectClearFlag(_Player, "LLLIB_ModMenu_HideNextPageButton");
ObjectClearFlag(_Player, "LLLIB_ModMenu_HidePreviousPageButton");

PROC
LLLIB_ModMenu_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page >= _LastPageIndex
AND
_MaxPage == 2
THEN
ObjectSetFlag(_Player, "LLLIB_ModMenu_HideNextPageButton", _Instance);
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu] Hid next page button");

/*
PROC
LLLIB_ModMenu_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
IntegertoString(_LastPageIndex, _LastIndexStr)
AND
IntegertoString(_MaxPage, _MaxPageStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB] ", "Test: LastIndex: ", _LastIndexStr," | Max: ",_MaxPageStr);
*/

PROC
LLLIB_ModMenu_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page <= 0
AND
_MaxPage == 2
THEN
ObjectSetFlag(_Player, "LLLIB_ModMenu_HidePreviousPageButton", _Instance);

PROC
LLLIB_ModMenu_NextPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page < _LastPageIndex
AND
IntegerSum(_Page, 1, _NextPage)
THEN
LLLIB_ModMenu_SetPage(_Player, _NextPage, _Instance);
NOT DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance);

PROC
LLLIB_ModMenu_NextPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page >= _LastPageIndex
THEN
LLLIB_ModMenu_SetPage(_Player, 0, _Instance);
NOT DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance);

PROC
LLLIB_ModMenu_PreviousPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
AND
_Page <= 0
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
THEN
LLLIB_ModMenu_SetPage(_Player, _LastPageIndex, _Instance);
NOT DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance);

PROC
LLLIB_ModMenu_PreviousPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
AND
_Page > 0
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
IntegerSubtract(_Page, 1, _NextPage)
THEN
LLLIB_ModMenu_SetPage(_Player, _NextPage, _Instance);
NOT DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance);
//END_REGION

//REGION PAGE_VARIABLES
PROC
LLLIB_ModMenu_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLLIB_ModMenu_PageMax(_,_,_)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu][ERROR] ", "Max page settings not set. Reconfiguring.");
LLLIB_ModMenu_CalculateMaxPage();

PROC
LLLIB_ModMenu_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _)
THEN
LLLIB_ModMenu_SetPage(_Player, 0, _Instance);
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu][ERROR] ", "Page not set for some reason. Setting to page 0.");

PROC
LLLIB_ModMenu_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _ID)
THEN
NOT DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _ID);

PROC
LLLIB_ModMenu_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModMenu_MenusPerPage(_MenusPerPage)
AND
IntegerProduct(_Page, _MenusPerPage, _StartIndex)
AND
DB_LLLIB_ModMenu_MenuVars(_MenuIndex, _DialogVar, _SelectFlag, _DisabledFlag)
AND
IntegerSum(_MenuIndex, _StartIndex, _Index)
AND
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable(_Player, _Index, _DisabledFlag)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _ID, _DisplayName)
//DB_LLLIB_ModMenu_RegisteredMenu(_Index, _ID, _DisplayName, _Dialog, _ModName, _Author)
AND
NOT DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _)
THEN
DialogSetVariableStringForInstance(_Instance, _DialogVar, _DisplayName);
DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _ID);

PROC
LLLIB_ModMenu_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
THEN
LLLIB_ModMenu_UpdatePageDisplay(_Player, _Instance);

PROC
LLLIB_ModMenu_UpdatePageDisplay((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_MaxPage > 1
AND
IntegerSum(_Page, 1, _DisplayedPage)
AND
IntegertoString(_DisplayedPage, _PageStr1)
AND
IntegertoString(_MaxPage, _PageStr2)
AND
StringConcatenate("Page: ", _PageStr1, _Str1)
AND
StringConcatenate(_Str1, "/", _Str2)
AND
StringConcatenate(_Str2, _PageStr2, _FinalStr)
AND
DB_LLLIB_Settings_DialogVariables("ModMenu_PageInfo", _VariableValue)
THEN
DialogSetVariableStringForInstance(_Instance, _VariableValue, _FinalStr);

PROC
LLLIB_ModMenu_UpdatePageDisplay((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModMenu_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_MaxPage <= 1
AND
DB_LLLIB_Settings_DialogVariables("ModMenu_PageInfo", _VariableValue)
THEN
DialogSetVariableStringForInstance(_Instance, _VariableValue, "");

QRY
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _, _)
AND
ObjectGetFlag(_Player, _DisabledFlag, 0)
THEN
ObjectSetFlag(_Player, _DisabledFlag);

QRY
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _ID, _DisplayName)
AND
DB_LLLIB_ModMenu_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
ObjectGetFlag(_Player, _DisabledFlag, 0)
AND
NOT LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
THEN
ObjectSetFlag(_Player, _DisabledFlag);

QRY
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _ID, _DisplayName)
AND
DB_LLLIB_ModMenu_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
ObjectGetFlag(_Player, _DisabledFlag, 1)
AND
LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
THEN
ObjectClearFlag(_Player, _DisabledFlag);

QRY
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
THEN
DB_NOOP(1);

PROC
LLLIB_ModMenu_CheckPageCount((CHARACTERGUID)_Player)
AND
NOT DB_LLLIB_ModMenu_PageMax(_, _, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModMenu] Page max not set?");

PROC
LLLIB_ModMenu_CheckPageCount((CHARACTERGUID)_Player)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_MaxPage > 1
THEN
ObjectSetFlag(_Player, "LLLIB_ModMenu_HasMultiplePages", 0);
//LeaderLog_Log("DEBUG", "[LLLIB:ModMenu] Multiple pages available.");

PROC
LLLIB_ModMenu_CheckPageCount((CHARACTERGUID)_Player)
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_MaxPage <= 1
THEN
ObjectClearFlag(_Player, "LLLIB_ModMenu_HasMultiplePages");
//LeaderLog_Log("DEBUG", "[LLLIB:ModMenu] Only one page available.");
//END_REGION

//REGION FLAG_ACTIONS
PROC
LLLIB_ModMenu_SetNextMenu((CHARACTERGUID)_Player, (STRING)_ID, (STRING)_Dialog)
AND
DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _LastID, _LastDialog)
THEN
NOT DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _LastID, _LastDialog);

PROC
LLLIB_ModMenu_SetNextMenu((CHARACTERGUID)_Player, (STRING)_ID, (STRING)_Dialog)
THEN
DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _ID, _Dialog);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Player, _Instance)
AND
DB_LLLIB_ModMenu_MenuVars(_MenuIndex, _DialogVar, _SelectFlag, _DisabledFlag)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, _SelectFlag)
AND
DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _ID)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _MenuIndex, _ID, _DisplayName)
AND
DB_LLLIB_ModMenu_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB] Loading menu for ", _ID);
LLLIB_ModMenu_SetNextMenu(_Player, _ID, _Dialog);

IF
ObjectFlagSet("LLLIB_ModMenu_NextPage", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_ModMenu_NextPage")
THEN
DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance);
LLLIB_ModMenu_NextPage(_Player, _Instance);
LLLIB_ModMenu_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLLIB_ModMenu_PreviousPage", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_ModMenu_PreviousPage")
THEN
DB_LLLIB_ModMenu_Temp_IsChangingMenuPage(_Player, _Instance);
LLLIB_ModMenu_PreviousPage(_Player, _Instance);
LLLIB_ModMenu_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLLIB_ModMenu_FirstPage", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_ModMenu_FirstPage")
THEN
LLLIB_ModMenu_SetPage(_Player, 0, _Instance);
LLLIB_ModMenu_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLLIB_ModMenu_LastPage", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_ModMenu_LastPage")
AND
DB_LLLIB_ModMenu_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
THEN
LLLIB_ModMenu_SetPage(_Player, _LastPageIndex, _Instance);
LLLIB_ModMenu_LoadPageVariables(_Player, _Instance);
//END_REGION

//REGION DIALOG_EVENTS
IF
DialogStarted("LLLIB_ModMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
LLLIB_ModMenu_ResetPlayerPage(_Player, _Instance);
LLLIB_ModMenu_LoadPageVariables(_Player, _Instance);
LLLIB_ModMenu_CheckPageCount(_Player);

IF
DialogEnded("LLLIB_ModMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _ID, _Dialog)
THEN
Proc_StartDialog(0, _Dialog, _Player, _Player);

IF
DialogEnded(_Dialog, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
UserGetFlag(_Player, "LeaderLib_AutoOpenModMenu", 1)
AND
ObjectGetFlag(_Player, "LeaderLib_BlockModMenuReopen", 0)
THEN
LLLIB_ModMenu_Reopen(_Player, _Dialog);

IF
DialogEnded(_Dialog, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "LeaderLib_OpenModMenu", 1)
THEN
ObjectClearFlag(_Player, "LeaderLib_OpenModMenu");
LLLIB_ModMenu_Reopen(_Player, _Dialog);

PROC
LLLIB_ModMenu_Reopen((CHARACTERGUID)_Player, (STRING)_Dialog)
AND
DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _ID, _Dialog)
THEN
NOT DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _ID, _Dialog);
Proc_StartDialog(0, "LLLIB_ModMenu", _Player, _Player);

IF
ObjectFlagSet("LeaderLib_ModMenu_ClearMemory", (CHARACTERGUID)_Player, _)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LeaderLib_ModMenu_ClearMemory")
AND
DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _ID, _Dialog)
THEN
NOT DB_LLLIB_ModMenu_Temp_ActiveMenu(_Player, _ID, _Dialog);
//END_REGION

//REGION ITEM_USE
IF
CharacterUsedItemTemplate(_Player, _ItemTemplate, _)
AND
DB_LLLIB_Settings_ItemTemplates("Book_ModMenu", _ItemTemplate)
AND
CharacterIsInCombat(_Player, 1)
THEN
CharacterStatusText(_Player, "<font color='#FF0000'>Unable to open Mod Settings menu in combat.</font>");

IF
CharacterUsedItemTemplate(_Player, _ItemTemplate, _Book)
AND
DB_LLLIB_Settings_ItemTemplates("Book_ModMenu", _ItemTemplate)
AND
CharacterIsInCombat(_Player, 0)
THEN
Proc_StartDialog(0, "LLLIB_ModMenu", _Book, _Player);
//END_REGION

//REGION UPDATER
IF
StoryEvent(_,"LLLIB_Events_OnUpdateSettings")
AND
LLLIB_Updater_QRY_SettingsNeedsUpdate("LLLIB_Settings_ModMenu")
THEN
LLLIB_ModMenu_InitSettings();
LeaderLog_Log("SYSTEM", "[LLLIB:ModMenu] Settings updated.");

PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModMenu", "DB_LLLIB_ModMenu_MenuVars", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModMenu", "DB_LLLIB_ModMenu_MenusPerPage", 1);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModMenu", "DB_LLLIB_ModMenu_RegisteredMenuData", 5);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModMenu", "DB_LLLIB_ModMenu_CurrentPageIndex", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModMenu", "DB_LLLIB_ModMenu_PageMax", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModMenu", "DB_LLLIB_ModMenu_Temp_ActiveMenu", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModMenu", "DB_LLLIB_ModMenu_Temp_IsChangingMenuPage", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "ModMenu", "DB_LLLIB_ModMenu_Temp_SortStarted", 1);
LeaderLog_Log("SYSTEM", "[LLLIB:ModMenu] Databases registered.");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
