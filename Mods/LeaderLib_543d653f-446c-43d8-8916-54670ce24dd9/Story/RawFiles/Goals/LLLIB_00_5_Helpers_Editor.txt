Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION ANIM_TEXT_EVENTS
//Workaround for the GetTextEvent bug
//http://larian.com/forums/ubbthreads.php?ubb=showflat&Number=643095
IF
TextEventSet(_Event)
AND
GetTextEventParamString(1, "leaderlib_anim") // What was formerly the text event name
AND
GetTextEventParamUUID(1, _Target)
AND
GetTextEventParamString(2, _Anim)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Helpers:Editor:TextEventSet] Event [leaderlib_anim] fired. Playing animation [",_Anim,"].");
PlayAnimation(_Target, _Anim);

IF
TextEventSet(_Event)
AND
GetTextEventParamString(1, "leaderlib_animloop")
AND
GetTextEventParamUUID(1, (CHARACTERGUID)_Target)
AND
GetTextEventParamString(2, _Anim)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Helpers:Editor:TextEventSet] Event [leaderlib_animloop] fired. Setting animation override to [",_Anim,"].");
CharacterSetAnimationOverride(_Target, _Anim);

IF
TextEventSet(_Event)
AND
GetTextEventParamString(1, "leaderlib_stopanims")
AND
GetTextEventParamUUID(1, (CHARACTERGUID)_Target)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Helpers:Editor:TextEventSet] Event [leaderlib_stopanims] fired. Clearing animation override.");
CharacterSetAnimationOverride(_Target, "");
//END_REGION

//REGION STATS_TEXT_EVENTS
IF
TextEventSet("leaderlib_addpoints")
AND
GetTextEventParamString(1, "attribute")
AND
GetTextEventParamInteger(2, _Amount)
AND
IntegertoString(_Amount, _AmountStr)
AND
DB_IsPlayer(_Player)
AND
CharacterIsControlled(_Player, 1)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Helpers:Editor:TextEventSet] Event [leaderlib_addpoints] fired. Adding [",_AmountStr,"] attribute points to controller character.");
CharacterAddAttributePoint(_Player, _Amount);

IF
TextEventSet("leaderlib_addpoints")
AND
GetTextEventParamString(1, "ability")
AND
GetTextEventParamInteger(2, _Amount)
AND
IntegertoString(_Amount, _AmountStr)
AND
DB_IsPlayer(_Player)
AND
CharacterIsControlled(_Player, 1)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Helpers:Editor:TextEventSet] Event [leaderlib_addpoints] fired. Adding [",_AmountStr,"] ability points to controller character.");
CharacterAddAbilityPoint(_Player, _Amount);

IF
TextEventSet("leaderlib_addpoints")
AND
GetTextEventParamString(1, "civil")
AND
GetTextEventParamInteger(2, _Amount)
AND
IntegertoString(_Amount, _AmountStr)
AND
DB_IsPlayer(_Player)
AND
CharacterIsControlled(_Player, 1)
THEN
LeaderLog_Log("DEBUG", "[LeaderLib:Helpers:Editor:TextEventSet] Event [leaderlib_addpoints] fired. Adding [",_AmountStr,"] civil points to controller character.");
CharacterAddCivilAbilityPoint(_Player, _Amount);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
