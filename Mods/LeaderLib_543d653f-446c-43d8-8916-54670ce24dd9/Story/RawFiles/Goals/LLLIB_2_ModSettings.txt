Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_ModSettings_InitSettings();

//Settings
//DB_LLLIB_Settings_DialogVariables(_VariableName, _VariableValue)
//DB_LLLIB_ModSettings_DynamicMenuVars(_MenuIndex, _DialogVar, _SelectFlag, _DisabledFlag)
//DB_LLLIB_ModSettings_MenusPerPage(_MaxPerPage)
//DB_LLLIB_ModSettings_SortTickRate(_Rate)
//DB_LLLIB_ModSettings_MenuSortTimerActive(1)
//DB_LLLIB_ModSettings_SortingStarted(1)
//DB_LLLIB_ModSettings_SortingMenuTimer(_TimerName, _ID, _DisplayName, _CurrentIndex)

//Dynamic
//DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _ID, _DisplayName)
//DB_LLLIB_ModSettings_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
//DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
//DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
//DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _ID, _Dialog)
//DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance)

//DB_LLLIB_ModSettings_Temp_PreviousMenu(_Index, _ID, _DisplayName, _Dialog, _ModName, _Author)
//DB_LLLIB_ModSettings_Temp_SortedMenu(_Index, _ID, _Dialog, _DisplayName)
//DB_LLLIB_ModSettings_Temp_UnsortedMenu(_ID, _DisplayName, _Dialog, _ModName, _Author)
KBSECTION
//REGION MENU_REGISTERING
PROC
LeaderLib_RegisterModMenu((STRING)_ID, (STRING)_DisplayName, (STRING)_Dialog, (STRING)_ModName, (STRING)_Author)
AND
NOT DB_LLLIB_ModSettings_RegisteredMenuData(_ID,_,_,_,_)
THEN
LLLIB_Sorter_DetermineStringLength(_DisplayName);
DB_LLLIB_ModSettings_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author);
//END_REGION

//REGION PAGE_MAX_CALCULATION
IF
TimerFinished("LLLIB_Timers_ModSettings_CalculateMaxPage")
THEN
LLLIB_ModSettings_CalculateMaxPage();

PROC
LLLIB_ModSettings_CalculateMaxPage()
THEN
LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_ModSettings_PageMax", 3);

PROC
LLLIB_ModSettings_CalculateMaxPage()
AND
SysCount("DB_LLLIB_ModSettings_RegisteredMenuData", 5, _TotalMenus)
AND
DB_LLLIB_ModSettings_MenusPerPage(_MaxPerPage)
AND
IntegerSubtract(_MaxPerPage, 1, _Variance)
AND
IntegerSum(_TotalMenus, _Variance, _PageNumToDivide)
AND
IntegerDivide(_PageNumToDivide, _MaxPerPage, _MaxPage)
AND
IntegerSubtract(_TotalMenus, 1, _LastIndex)
AND
IntegerSubtract(_MaxPage, 1, _LastPageIndex)
THEN
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage);

/*
IF
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
IntegertoString(_LastIndex, _Str1)
AND
IntegertoString(_LastPageIndex, _Str2)
AND
IntegertoString(_MaxPage, _Str3)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Max page set: LastIndex[",_Str1,"] LastPageIndex[",_Str2,"] MaxPage[",_Str3,"]");
*/
//END_REGION

//REGION SETTINGS
PROC
LLLIB_ModSettings_InitSettings()
THEN
LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_ModSettings_SortTickRate", 1);
DB_LLLIB_ModSettings_SortTickRate(1);

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_ModSettings_MenusPerPage", 1);
DB_LLLIB_ModSettings_MenusPerPage(8);

LeaderLib_RegisterModMenu("LaughingLeader_LeaderLib", "[LeaderLib] Settings", "LLLIB_SettingsMenu", "LeaderLib", "LaughingLeader");

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_ModSettings_DynamicMenuVars", 4);
DB_LLLIB_ModSettings_DynamicMenuVars(0, "LLLIB_ModSettings_Menu1_783ec14d-2861-4345-8dbc-45974fa34e15", "LLLIB_ModSettings_SelectedMenu1", "LLLIB_ModSettings_MenuEmpty1");
DB_LLLIB_ModSettings_DynamicMenuVars(1, "LLLIB_ModSettings_Menu2_9a2d2990-5cff-420e-ba8a-37f9510ab9e1", "LLLIB_ModSettings_SelectedMenu2", "LLLIB_ModSettings_MenuEmpty2");
DB_LLLIB_ModSettings_DynamicMenuVars(2, "LLLIB_ModSettings_Menu3_cf00a8c6-02d2-4fda-a766-4f533ac93b2d", "LLLIB_ModSettings_SelectedMenu3", "LLLIB_ModSettings_MenuEmpty3");
DB_LLLIB_ModSettings_DynamicMenuVars(3, "LLLIB_ModSettings_Menu4_337a1066-9a85-4005-a2dc-f0156bf13d16", "LLLIB_ModSettings_SelectedMenu4", "LLLIB_ModSettings_MenuEmpty4");
DB_LLLIB_ModSettings_DynamicMenuVars(4, "LLLIB_ModSettings_Menu5_45148ce6-e8f3-4af4-81ed-a3b2e1ff8a17", "LLLIB_ModSettings_SelectedMenu5", "LLLIB_ModSettings_MenuEmpty5");
DB_LLLIB_ModSettings_DynamicMenuVars(5, "LLLIB_ModSettings_Menu6_863af621-ff0c-454a-b6a3-7c9d3166a8a0", "LLLIB_ModSettings_SelectedMenu6", "LLLIB_ModSettings_MenuEmpty6");
DB_LLLIB_ModSettings_DynamicMenuVars(6, "LLLIB_ModSettings_Menu7_888ce02f-83ff-4173-be8d-f41fd348358f", "LLLIB_ModSettings_SelectedMenu7", "LLLIB_ModSettings_MenuEmpty7");
DB_LLLIB_ModSettings_DynamicMenuVars(7, "LLLIB_ModSettings_Menu8_a6aa98e6-cde4-491b-a2cb-2cd7d5d71f96", "LLLIB_ModSettings_SelectedMenu8", "LLLIB_ModSettings_MenuEmpty8");
//END_REGION

//REGION UPDATING
PROC
LLLIB_System_UpdateDatabases()
THEN
LLLIB_ModSettings_InitSettings();
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Databases updated.");

PROC
LLLIB_System_ClearDatabases()
THEN
SysClear("DB_LLLIB_ModSettings_MenusPerPage", 1);
SysClear("DB_LLLIB_ModSettings_RegisteredMenuData", 5);
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Databases cleared.");

PROC
LLLIB_Updater_ResetDatabases()
THEN
LLLIB_System_ClearDatabases();
LLLIB_System_UpdateDatabases();
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Databases cleared and re-initialized.");
//END_REGION

//REGION SORTING
//Sorting Start
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
DB_LLLIB_ModSettings_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
NOT DB_LLLIB_ModSettings_SortingStarted(_)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
THEN
TimerLaunch("LLLIB_Timers_ModSettings_SortMenus", 500);
DB_LLLIB_ModSettings_SortingStarted(1);

PROC
LLLIB_ModSettings_SortingComplete()
AND
DB_LLLIB_ModSettings_SortingStarted(_Val)
THEN
NOT DB_LLLIB_ModSettings_SortingStarted(_Val);

PROC
LLLIB_ModSettings_SortingComplete()
AND
DB_LLLIB_ModSettings_CurrentlySorting(_Val)
THEN
NOT DB_LLLIB_ModSettings_CurrentlySorting(_Val);

PROC
LLLIB_ModSettings_SortingComplete()
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings:SortingComplete] Menu sorting done.");
TimerCancel("LLLIB_Timers_ModSettings_CalculateMaxPage");
TimerLaunch("LLLIB_Timers_ModSettings_CalculateMaxPage", 1000);

IF
TimerFinished("LLLIB_Timers_ModSettings_SortMenus")
AND
DB_LLLIB_ModSettings_CurrentlySorting(_Val)
THEN
NOT DB_LLLIB_ModSettings_CurrentlySorting(_Val);

IF
TimerFinished("LLLIB_Timers_ModSettings_SortMenus")
AND
DB_LLLIB_ModSettings_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
NOT DB_LLLIB_ModSettings_CurrentlySorting(_)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
THEN
LLLIB_ModSettings_SortMenu(_ID, _DisplayName);
DB_LLLIB_ModSettings_CurrentlySorting(1);

PROC
LLLIB_ModSettings_OnMenuSorted((STRING)_ID)
AND
DB_LLLIB_ModSettings_SortingMenuTimer(_TimerName, _ID, _DisplayName, _CurrentIndex)
THEN
NOT DB_LLLIB_ModSettings_SortingMenuTimer(_TimerName, _ID, _DisplayName, _CurrentIndex);
TimerCancel(_TimerName);
//LeaderLog_Log("DEBUG", "[LLLIB:ModSettings:OnMenuSorted] Menu '",_DisplayName,"' was sorted successfully.");

PROC
LLLIB_ModSettings_OnMenuSorted((STRING)_ID)
AND
LLLIB_ModSettings_QRY_SortingNeeded()
AND
DB_LLLIB_ModSettings_SortTickRate(_Rate)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:ModSettings:OnMenuSorted] Starting sort timer. ");
TimerLaunch("LLLIB_Timers_ModSettings_SortMenus", _Rate);

PROC
LLLIB_ModSettings_OnMenuSorted((STRING)_ID)
AND
DB_LLLIB_ModSettings_CurrentlySorting(_Val)
THEN
NOT DB_LLLIB_ModSettings_CurrentlySorting(_Val);

PROC
LLLIB_ModSettings_OnMenuSorted((STRING)_ID)
AND
NOT LLLIB_ModSettings_QRY_SortingNeeded()
THEN
LLLIB_ModSettings_SortingComplete();

QRY
LLLIB_ModSettings_QRY_SortingNeeded()
AND
DB_LLLIB_ModSettings_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
THEN
DB_NOOP(1);

PROC
LLLIB_ModSettings_SortMenu((STRING)_ID, (STRING)_DisplayName)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _, _)
THEN
LLLIB_Array_AddToDictionary("LLLIB_RegisteredMenus", _ID, _DisplayName);
LLLIB_ModSettings_OnMenuSorted(_ID);

PROC
LLLIB_ModSettings_SortMenu((STRING)_ID, (STRING)_DisplayName)
AND
NOT DB_LLLIB_Array_Length("LLLIB_RegisteredMenus", _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] [ERROR] Dictionary 'LLLIB_RegisteredMenus' has no length entry!");

PROC
LLLIB_ModSettings_SortMenu((STRING)_ID, (STRING)_DisplayName)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
AND
DB_LLLIB_Array_Length("LLLIB_RegisteredMenus", _Length)
AND
_Length > 0
AND
StringConcatenate("LLLIB_Timers_MenuSettings_SortingMenu_", _ID, _TimerName)
THEN
DB_LLLIB_ModSettings_SortingMenuTimer(_TimerName, _ID, _DisplayName, 0);
//LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Finding sorted position for menu '",_DisplayName,"'.");
LLLIB_Array_BuildArrayIterator("LLLIB_RegisteredMenus");
LLLIB_ModSettings_Internal_FindNextMenuIndex(_ID, _DisplayName);

IF
TimerFinished(_TimerName)
AND
DB_LLLIB_ModSettings_SortingMenuTimer(_TimerName, _ID, _DisplayName, _CurrentIndex)
AND
IntegerSum(_CurrentIndex, 1, _NextIndex)
THEN
NOT DB_LLLIB_ModSettings_SortingMenuTimer(_TimerName, _ID, _DisplayName, _CurrentIndex);
DB_LLLIB_ModSettings_SortingMenuTimer(_TimerName, _ID, _DisplayName, _NextIndex);
LLLIB_ModSettings_Internal_FindNextMenuIndex(_ID, _DisplayName);

PROC
LLLIB_ModSettings_Internal_FindNextMenuIndex((STRING)_ID, (STRING)_DisplayName)
AND
DB_LLLIB_ModSettings_SortingMenuTimer(_TimerName, _ID, _DisplayName, _Index)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _DisplayName)
AND
DB_LLLIB_ModSettings_SortTickRate(_Rate)
THEN
LLLIB_ModSettings_Internal_MenuSort_OnIterator(_ID, _DisplayName, _Index);
TimerLaunch(_TimerName, _Rate);

/*
PROC
LLLIB_ModSettings_Internal_MenuSort_OnIterator((STRING)_ID, (STRING)_DisplayName, (INTEGER)_Index)
AND
IntegertoString(_Index, _CurrentIndexStr)
AND
DB_LLLIB_Array_Length("LLLIB_RegisteredMenus", _Length)
AND
IntegertoString(_Length, _LengthStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Iterating [",_ID,"]'",_DisplayName,"' index: [",_CurrentIndexStr,"/",_LengthStr,"].");
*/

PROC
LLLIB_ModSettings_Internal_MenuSort_OnIterator((STRING)_ID, (STRING)_DisplayName, (INTEGER)_Index)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _CheckID, _CheckDisplayName)
AND
LLLIB_Sorter_QRY_CompareStrings("LLLIB_RegisteredMenus", _DisplayName, _CheckDisplayName)
AND
DB_LLLIB_StringCompare_Results(_DisplayName, _CheckDisplayName, _ComparisonVal)
//IntegerSum(0,0,_ComparisonVal)
AND
IntegertoString(_ComparisonVal, _CompValStr)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:ModSettings:ProcessCompareResults] Comparison value: '",_DisplayName,"' => '",_CheckDisplayName,"' = ",_CompValStr);
LLLIB_ModSettings_Internal_ProcessCompareResults(_ID, _DisplayName, _Index, _ComparisonVal);

PROC
LLLIB_ModSettings_Internal_MenuSort_OnIterator((STRING)_ID, (STRING)_DisplayName, (INTEGER)_Index)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
AND
DB_LLLIB_Array_Length("LLLIB_RegisteredMenus", _Length)
AND
_Index >= _Length
THEN
LLLIB_Array_AddToDictionary("LLLIB_RegisteredMenus", _ID, _DisplayName);
LLLIB_ModSettings_OnMenuSorted(_ID);

PROC
LLLIB_ModSettings_Internal_ProcessCompareResults((STRING)_ID, (STRING)_DisplayName, (INTEGER)_CurrentIndex, (INTEGER)_ComparisonVal)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
AND
_ComparisonVal <= 0
THEN
LLLIB_Array_AddToDictionaryAtIndex("LLLIB_RegisteredMenus", _ID, _DisplayName, _CurrentIndex);
LLLIB_ModSettings_OnMenuSorted(_ID);

PROC
LLLIB_ModSettings_Internal_ProcessCompareResults((STRING)_ID, (STRING)_DisplayName, (INTEGER)_CurrentIndex, (INTEGER)_ComparisonVal)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
AND
_ComparisonVal > 0
AND
DB_LLLIB_Array_Length("LLLIB_RegisteredMenus", _Length)
AND
_CurrentIndex < _Length
THEN
//Keep iterating
DB_NOOP(1);

PROC
LLLIB_ModSettings_Internal_ProcessCompareResults((STRING)_ID, (STRING)_DisplayName, (INTEGER)_CurrentIndex, (INTEGER)_ComparisonVal)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _, _ID, _)
AND
_ComparisonVal > 0
AND
DB_LLLIB_Array_Length("LLLIB_RegisteredMenus", _Length)
AND
_CurrentIndex >= _Length
THEN
LLLIB_Array_AddToDictionary("LLLIB_RegisteredMenus", _ID, _DisplayName);
LLLIB_ModSettings_OnMenuSorted(_ID);
//END_REGION

//REGION CLEAR
PROC
LLLIB_ModSettings_ResetPlayerPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
THEN
NOT DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page);

PROC
LLLIB_ModSettings_ResetPlayerPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _)
THEN
LLLIB_ModSettings_SetPage(_Player, 0, _Instance);
//END_REGION

//REGION PAGE_TURNING
PROC
LLLIB_ModSettings_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _CurrentPage)
THEN
NOT DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _CurrentPage);

PROC
LLLIB_ModSettings_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
THEN
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page);
ObjectClearFlag(_Player, "LLLIB_ModSettings_HideNextPageButton");
ObjectClearFlag(_Player, "LLLIB_ModSettings_HidePreviousPageButton");

PROC
LLLIB_ModSettings_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page >= _LastPageIndex
AND
_MaxPage == 2
THEN
ObjectSetFlag(_Player, "LLLIB_ModSettings_HideNextPageButton", _Instance);
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Hid next page button");

/*
PROC
LLLIB_ModSettings_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
IntegertoString(_LastPageIndex, _LastIndexStr)
AND
IntegertoString(_MaxPage, _MaxPageStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB] ", "Test: LastIndex: ", _LastIndexStr," | Max: ",_MaxPageStr);
*/

PROC
LLLIB_ModSettings_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page <= 0
AND
_MaxPage == 2
THEN
ObjectSetFlag(_Player, "LLLIB_ModSettings_HidePreviousPageButton", _Instance);

PROC
LLLIB_ModSettings_NextPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page < _LastPageIndex
AND
IntegerSum(_Page, 1, _NextPage)
THEN
LLLIB_ModSettings_SetPage(_Player, _NextPage, _Instance);
NOT DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance);

PROC
LLLIB_ModSettings_NextPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page >= _LastPageIndex
THEN
LLLIB_ModSettings_SetPage(_Player, 0, _Instance);
NOT DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance);

PROC
LLLIB_ModSettings_PreviousPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
AND
_Page <= 0
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
THEN
LLLIB_ModSettings_SetPage(_Player, _LastPageIndex, _Instance);
NOT DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance);

PROC
LLLIB_ModSettings_PreviousPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
AND
_Page > 0
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
IntegerSubtract(_Page, 1, _NextPage)
THEN
LLLIB_ModSettings_SetPage(_Player, _NextPage, _Instance);
NOT DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance);
//END_REGION

//REGION PAGE_VARIABLES
PROC
LLLIB_ModSettings_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLLIB_ModSettings_PageMax(_,_,_)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings][ERROR] ", "Max page settings not set. Reconfiguring.");
LLLIB_ModSettings_CalculateMaxPage();

PROC
LLLIB_ModSettings_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _)
THEN
LLLIB_ModSettings_SetPage(_Player, 0, _Instance);
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings][ERROR] ", "Page not set for some reason. Setting to page 0.");

PROC
LLLIB_ModSettings_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _ID)
THEN
NOT DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _ID);

PROC
LLLIB_ModSettings_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModSettings_MenusPerPage(_MenusPerPage)
AND
IntegerProduct(_Page, _MenusPerPage, _StartIndex)
AND
DB_LLLIB_ModSettings_DynamicMenuVars(_MenuIndex, _DialogVar, _SelectFlag, _DisabledFlag)
AND
IntegerSum(_MenuIndex, _StartIndex, _Index)
AND
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable(_Player, _Index, _DisabledFlag)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _ID, _DisplayName)
//DB_LLLIB_ModSettings_RegisteredMenu(_Index, _ID, _DisplayName, _Dialog, _ModName, _Author)
AND
NOT DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _)
THEN
DialogSetVariableStringForInstance(_Instance, _DialogVar, _DisplayName);
DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _ID);

PROC
LLLIB_ModSettings_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
THEN
LLLIB_ModSettings_UpdatePageDisplay(_Player, _Instance);

PROC
LLLIB_ModSettings_UpdatePageDisplay((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_MaxPage > 1
AND
IntegerSum(_Page, 1, _DisplayedPage)
AND
IntegertoString(_DisplayedPage, _PageStr1)
AND
IntegertoString(_MaxPage, _PageStr2)
AND
StringConcatenate("Page: ", _PageStr1, _Str1)
AND
StringConcatenate(_Str1, "/", _Str2)
AND
StringConcatenate(_Str2, _PageStr2, _FinalStr)
AND
DB_LLLIB_Settings_DialogVariables("ModSettings_PageInfo", _VariableValue)
THEN
DialogSetVariableStringForInstance(_Instance, _VariableValue, _FinalStr);

PROC
LLLIB_ModSettings_UpdatePageDisplay((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLLIB_ModSettings_CurrentPageIndex(_Player, _Page)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_MaxPage <= 1
AND
DB_LLLIB_Settings_DialogVariables("ModSettings_PageInfo", _VariableValue)
THEN
DialogSetVariableStringForInstance(_Instance, _VariableValue, "");

QRY
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
AND
NOT DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _, _)
AND
ObjectGetFlag(_Player, _DisabledFlag, 0)
THEN
ObjectSetFlag(_Player, _DisabledFlag);

QRY
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _ID, _DisplayName)
AND
DB_LLLIB_ModSettings_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
ObjectGetFlag(_Player, _DisabledFlag, 0)
AND
NOT LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
THEN
ObjectSetFlag(_Player, _DisabledFlag);

QRY
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _Index, _ID, _DisplayName)
AND
DB_LLLIB_ModSettings_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
AND
ObjectGetFlag(_Player, _DisabledFlag, 1)
AND
LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
THEN
ObjectClearFlag(_Player, _DisabledFlag);

QRY
LLLIB_QRY_WordMenus_DisableMenuIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
THEN
DB_NOOP(1);

PROC
LLLIB_ModSettings_CheckPageCount((CHARACTERGUID)_Player)
AND
NOT DB_LLLIB_ModSettings_PageMax(_, _, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Page max not set?");

PROC
LLLIB_ModSettings_CheckPageCount((CHARACTERGUID)_Player)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_MaxPage > 1
THEN
ObjectSetFlag(_Player, "LLLIB_ModSettings_HasMultiplePages", 0);
//LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Multiple pages available.");

PROC
LLLIB_ModSettings_CheckPageCount((CHARACTERGUID)_Player)
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
AND
_MaxPage <= 1
THEN
ObjectClearFlag(_Player, "LLLIB_ModSettings_HasMultiplePages");
//LeaderLog_Log("DEBUG", "[LLLIB:ModSettings] Only one page available.");
//END_REGION

//REGION FLAG_ACTIONS
PROC
LLLIB_ModSettings_SetNextMenu((CHARACTERGUID)_Player, (STRING)_ID, (STRING)_Dialog)
AND
DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _LastID, _LastDialog)
THEN
NOT DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _LastID, _LastDialog);

PROC
LLLIB_ModSettings_SetNextMenu((CHARACTERGUID)_Player, (STRING)_ID, (STRING)_Dialog)
THEN
DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _ID, _Dialog);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Player, _Instance)
AND
DB_LLLIB_ModSettings_DynamicMenuVars(_MenuIndex, _DialogVar, _SelectFlag, _DisabledFlag)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, _SelectFlag)
AND
DB_LLLIB_MenuSettings_Temp_MenuVariableValue(_MenuIndex, _ID)
AND
DB_LLLIB_Dictionary_Data("LLLIB_RegisteredMenus", _MenuIndex, _ID, _DisplayName)
AND
DB_LLLIB_ModSettings_RegisteredMenuData(_ID, _DisplayName, _Dialog, _ModName, _Author)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB] Loading menu for ", _ID);
LLLIB_ModSettings_SetNextMenu(_Player, _ID, _Dialog);

IF
ObjectFlagSet("LLLIB_ModSettings_NextPage", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_ModSettings_NextPage")
THEN
DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance);
LLLIB_ModSettings_NextPage(_Player, _Instance);
LLLIB_ModSettings_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLLIB_ModSettings_PreviousPage", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_ModSettings_PreviousPage")
THEN
DB_LLLIB_ModSettings_Temp_IsChangingMenuPage(_Player, _Instance);
LLLIB_ModSettings_PreviousPage(_Player, _Instance);
LLLIB_ModSettings_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLLIB_ModSettings_FirstPage", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_ModSettings_FirstPage")
THEN
LLLIB_ModSettings_SetPage(_Player, 0, _Instance);
LLLIB_ModSettings_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLLIB_ModSettings_LastPage", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_ModSettings_LastPage")
AND
DB_LLLIB_ModSettings_PageMax(_LastIndex, _LastPageIndex, _MaxPage)
THEN
LLLIB_ModSettings_SetPage(_Player, _LastPageIndex, _Instance);
LLLIB_ModSettings_LoadPageVariables(_Player, _Instance);
//END_REGION

//REGION DIALOG_EVENTS
IF
DialogStarted("LLLIB_ModSettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
LLLIB_ModSettings_ResetPlayerPage(_Player, _Instance);
LLLIB_ModSettings_LoadPageVariables(_Player, _Instance);
LLLIB_ModSettings_CheckPageCount(_Player);

IF
DialogEnded("LLLIB_ModSettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _ID, _Dialog)
THEN
Proc_StartDialog(0, _Dialog, _Player, _Player);

IF
DialogEnded(_Dialog, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
UserGetFlag(_Player, "LeaderLib_AutoOpenModSettingsMenu", 1)
AND
ObjectGetFlag(_Player, "LeaderLib_OpenModSettingsMenu", 1)
THEN
LLLIB_ModSettings_Reopen(_Player, _Dialog);
ObjectClearFlag(_Player, "LeaderLib_OpenModSettingsMenu");

PROC
LLLIB_ModSettings_Reopen((CHARACTERGUID)_Player, (STRING)_Dialog)
AND
DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _ID, _Dialog)
THEN
NOT DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _ID, _Dialog);
Proc_StartDialog(0, "LLLIB_ModSettingsMenu", _Player, _Player);

IF
ObjectFlagSet("LeaderLib_ModSettingsMenu_ClearMemory", (CHARACTERGUID)_Player, _)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LeaderLib_ModSettingsMenu_ClearMemory")
AND
DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _ID, _Dialog)
THEN
NOT DB_LLLIB_ModSettings_Temp_ActiveMenu(_Player, _ID, _Dialog);
//END_REGION

//REGION ITEM_USE
IF
CharacterUsedItemTemplate(_Player, _ItemTemplate, _)
AND
DB_LLLIB_Settings_ItemTemplates("Book_ModSettings", _ItemTemplate)
AND
CharacterIsInCombat(_Player, 1)
THEN
CharacterStatusText(_Player, "<font color='#FF0000'>Unable to open Mod Settings menu in combat.</font>");

IF
CharacterUsedItemTemplate(_Player, _ItemTemplate, _Book)
AND
DB_LLLIB_Settings_ItemTemplates("Book_ModSettings", _ItemTemplate)
AND
CharacterIsInCombat(_Player, 0)
THEN
Proc_StartDialog(0, "LLLIB_ModSettingsMenu", _Book, _Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
