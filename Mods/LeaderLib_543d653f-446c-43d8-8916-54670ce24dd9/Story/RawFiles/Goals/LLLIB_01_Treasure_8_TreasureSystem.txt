Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION REGISTERED
//DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object)
//DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _Template)
//DB_LLLIB_Treasure_RegisteredTrader(_TreasureID, _TraderID)
//DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel)

//DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID)
//DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID)
//DB_LLLIB_Treasure_ItemGenerationSettings(_TreasureID, _ItemEntry, _MinLevel, _MaxLevel, _UsePartyLevel)
//DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType)
//DB_LLLIB_Treasure_RegisteredGenerationTypes(_GenerationType)
//DB_LLLIB_Treasure_ItemGenerationFlag(_TreasureID, _ItemEntry, _Flag, _FlagType)
//DB_LLLIB_Treasure_ItemDeltaMods(_TreasureID, _ItemEntry, _Deltamod, _Chance)
//DB_LLLIB_Treasure_ItemRunes(_TreasureID, _ItemEntry, _Rune, _Chance)
//DB_LLLIB_Treasure_ItemMaxAmount(_TreasureID, _ItemEntry, _MaxAmount)

//DB_LLLIB_Treasure_OnlyOnce(_TreasureID, _ItemEntryOrTreasureTable, _OnCompletionEvent)
//DB_LLLIB_Treasure_GeneratedEvent(_TreasureID, _ItemEntry, _OnGeneratedEvent)
//END_REGION
//DB_LLLIB_Treasure_StatToTemplate(_ItemEntry, _ItemTemplate)
//DB_LLLIB_Treasure_GeneratorDummies(_Dummy)

//DB_LLLIB_Treasure_Temp_DummyTarget(_Object, _Dummy, _TreasureID, _ItemEntry)
//DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
//DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID)
//DB_LLLIB_Treasure_Temp_FlagToTreasure(_Flag, _FlagType, _TreasureID)
//DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure(_GenerationType, _TreasureID)
//DB_LLLIB_Treasure_Temp_AmountToCreate(_Object, _ItemEntry, _AmountToCreate)
//DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, _CreatedAmount, _MaxAmount, _IsStat)
//DB_LLLIB_Treasure_Temp_Queue_CountTemplate(_Object, _TreasureID, _ItemEntry, _GeneratedAmount, _RequirementID)
//DB_LLLIB_Treasure_Temp_Queue_CountStat(_Object, _TreasureID, _ItemTemplate, _ItemEntry, _GeneratedAmount, _RequirementID)
//DB_LLLIB_Treasure_Temp_StartGeneration(_Object, _TreasureID, _TimerName)
/*GENERATION_QUEUE*/
//DB_LLLIB_Treasure_Temp_QueueTimerActive(_Active, _TickRate)
//DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType)
//DB_LLLIB_Array_Data("LLLIB_ItemGenerationQueue", _Index, _ID)
//DB_LLLIB_Treasure_Temp_ItemGenerationQueue(_ID, _Object, _TreasureID, _GenerationType)
KBSECTION
//REGION UPDATER
PROC
LLLIB_System_UpdateDatabases()
THEN
DB_NOOP(1);

PROC
LLLIB_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_StatToTemplate", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_DummyTarget", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_MatchedTreasure", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_FlagToTreasure", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_AmountToCreate", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_GenerateItem", 7);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_Queue_CountTemplate", 5);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_Queue_CountStat", 6);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_StartGeneration", 3);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_QueueTimerActive", 2);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_TimeoutTimerActive", 4);
LeaderUpdater_Register_Database("LeaderLib", "LaughingLeader", "TreasureSystem", "DB_LLLIB_Treasure_Temp_ItemGenerationQueue", 4);
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem] Databases registered.");
//END_REGION

//REGION GETTERS
QRY
LLLIB_QRY_Treasure_GetTreasureID((GUIDSTRING)_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureID((GUIDSTRING)_Object)
AND
DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object)
THEN
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureID((GUIDSTRING)_Object)
AND
GetTemplate(_Object, _Template)
AND
DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _Template)
THEN
DB_LLLIB_Treasure_Temp_MatchedTreasure((GUIDSTRING)_Object, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureID((GUIDSTRING)_Object)
AND
DB_LLLIB_Traders_Spawned((CHARACTERGUID)_Object, _TraderID, _Level)
AND
DB_LLLIB_Treasure_RegisteredTrader(_TreasureID, _TraderID)
THEN
DB_LLLIB_Treasure_Temp_MatchedTreasure((GUIDSTRING)_Object, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureObject((STRING)_TreasureID, (STRING)_GenerationType)
THEN
LLLIB_Treasure_Internal_GetTreasureObject_ClearPreviousTemp(_TreasureID, _GenerationType);

PROC
LLLIB_Treasure_Internal_GetTreasureObject_ClearPreviousTemp((STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template, _GenerationType)
THEN
NOT DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template, _GenerationType);

PROC
LLLIB_Treasure_Internal_GetTreasureObject_ClearPreviousTemp((STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureObject((STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object)
THEN
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureObject((STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_RegisteredTrader(_TreasureID, _TraderID)
AND
DB_LLLIB_Traders_Spawned(_Trader, _TraderID, _Level)
THEN
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Trader, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureObject((STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _Template)
AND
NOT DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template, _GenerationType)
THEN
DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template, _GenerationType);
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem] Launching iterators to find object with template [",_Template,"] for [",_TreasureID,":",_GenerationType,"]");
CharacterLaunchIterator("LLLIB_Events_Treasure_FindObject");
ItemLaunchIterator("LLLIB_Events_Treasure_FindObject");

IF
StoryEvent(_Object, "LLLIB_Events_Treasure_FindObject")
AND
GetTemplate(_Object, _Template)
AND
DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template, _GenerationType)
THEN
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);
LeaderLib_SendEvent("LLLIB_Events_Treasure_ContinueGeneration");

/*
IF
StoryEvent(NULL_00000000-0000-0000-0000-000000000000, "LLLIB_Events_Treasure_FindObject")
AND
DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template)
THEN
NOT DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template);
*/

QRY
LLLIB_QRY_Treasure_GetTreasureObject((STRING)_TreasureID)
AND
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_, _TreasureID)
AND
DB_LLLIB_Treasure_RegisteredTrader(_TreasureID, _TraderID)
AND
DB_LLLIB_Traders_Spawned(_Object, _TraderID, _Level)
THEN
DB_LLLIB_Treasure_Temp_MatchedTreasure((GUIDSTRING)_Object, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureByGenerationType((STRING)_GenerationType)
AND
DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure(_GenerationType, _TreasureID)
THEN
NOT DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure(_GenerationType, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureByGenerationType((STRING)_GenerationType)
AND
DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType)
AND
NOT DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure(_GenerationType, _TreasureID)
THEN
DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure(_GenerationType, _TreasureID);

QRY
LLLIB_QRY_Treasure_GetTreasureByRequirement((STRING)_RequirementID)
AND
DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID)
THEN
NOT DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID);

QRY
LLLIB_QRY_Treasure_GetTreasureByRequirement((STRING)_RequirementID)
AND
DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID)
AND
NOT DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID)
THEN
DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID);

QRY
LLLIB_QRY_Treasure_GetTreasureByRequirement((STRING)_RequirementID)
AND
DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID)
AND
NOT DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID)
THEN
DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID);

QRY
LLLIB_QRY_Treasure_GetTreasureByRequirement((STRING)_RequirementID)
AND
DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel)
AND
NOT DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID)
THEN
DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID);
//END_REGION

//REGION QUERIES_CHECKS
QRY
LLLIB_QRY_Treasure_IsRegisteredObject((GUIDSTRING)_Object)
AND
DB_LLLIB_Treasure_RegisteredObject(_TreasureID, _Object)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_IsRegisteredObject((CHARACTERGUID)_Object)
AND
DB_LLLIB_Traders_Spawned(_Object, _TraderID, _Level)
AND
DB_LLLIB_Treasure_RegisteredTrader(_TreasureID, _TraderID)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_IsRegisteredObject((GUIDSTRING)_Object)
AND
NOT DB_LLLIB_Treasure_RegisteredObject(_, _Object)
AND
GetTemplate(_Object, _Template)
AND
DB_LLLIB_Treasure_RegisteredObjectTemplate(_TreasureID, _Template)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_GenerationTypeIsDefault("DEFAULT")
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_GenerationTypeIsDefault("MANUAL")
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_GenerationTypeIsDefault("OPENED")
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_GenerationTypeIsDefault("TRADE_GENERATION_END")
THEN
DB_NOOP(1);


QRY
LLLIB_QRY_Treasure_ItemCanGenerate((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_RequirementID, "MANUAL")
AND
LLLIB_QRY_Requirements_AllMet(_RequirementID)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_ItemCanGenerate((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_RequirementID, (STRING)_GenerationType)
AND
NOT DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _)
AND
LLLIB_QRY_Treasure_GenerationTypeIsDefault(_GenerationType)
AND
LLLIB_QRY_Requirements_AllMet(_RequirementID)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_ItemCanGenerate((STRING)_TreasureID, (STRING)_ItemEntry, (STRING)_RequirementID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _ItemEntry, _GenerationType)
AND
LLLIB_QRY_Requirements_AllMet(_RequirementID)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_ObjectCanStartGeneration((GUIDSTRING)_Object)
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_GenerationEnded", 0)
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 0)
THEN
DB_NOOP(1);

QRY
LLLIB_QRY_Treasure_TreasureHasGenerationType((STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_ItemGenerationType(_TreasureID, _, _GenerationType)
THEN
DB_NOOP(1);
//END_REGION

//REGION TREASURE_TABLE_GENERATION
PROC
LLLIB_Treasure_GenerateItems((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel)
AND
LLLIB_QRY_Requirements_AllMet(_RequirementID)
AND
NOT DB_LLLIB_Treasure_TableWasGenerated(_Object, _TreasureID, _TreasureTable)
AND
_UsePartyLevel > 0
AND
LLLIB_Helper_QRY_GetHighestLevelInParty()
AND
DB_LLLIB_Helper_Temp_HighestLevelInParty(_Level)
AND
CreateItemTemplateAtPosition("LOOT_BackPack_A_6c70c298-aa29-418f-a659-f8e0b5f5fa60", 0.0,0.0,0.0, _Backpack)
THEN
GenerateTreasure(_Backpack, _TreasureTable, _Level, NULL_00000000-0000-0000-0000-000000000000);
MoveAllItemsTo(_Backpack, _Object, 0, 0, 1);
ItemDestroy(_Backpack);
SetOnStage(_Backpack, 0);
LLLIB_Treasure_Internal_OnTreasureTableGenerated(_ID, _Object, _TreasureID, _TreasureTable);

PROC
LLLIB_Treasure_GenerateItems((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_RegisteredTreasureTable(_TreasureID, _TreasureTable, _RequirementID, _UsePartyLevel, _MinLevel, _MaxLevel)
AND
LLLIB_QRY_Requirements_AllMet(_RequirementID)
AND
NOT DB_LLLIB_Treasure_TableWasGenerated(_Object, _TreasureID, _TreasureTable)
AND
_UsePartyLevel <= 0
AND
LLLIB_Random(_MinLevel, _MaxLevel)
AND
DB_LLLIB_Temp_RandomResult(_Level)
AND
CreateItemTemplateAtPosition("LOOT_BackPack_A_6c70c298-aa29-418f-a659-f8e0b5f5fa60", 0.0,0.0,0.0, _Backpack)
THEN
GenerateTreasure(_Backpack, _TreasureTable, _Level, NULL_00000000-0000-0000-0000-000000000000);
MoveAllItemsTo(_Backpack, _Object, 0, 0, 1);
ItemDestroy(_Backpack);
SetOnStage(_Backpack, 0);
LLLIB_Treasure_Internal_OnTreasureTableGenerated(_ID, _Object, _TreasureID, _TreasureTable);

PROC
LLLIB_Treasure_Internal_OnTreasureTableGenerated((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_TreasureTable)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:OnTreasureTableGenerated] Generated treasure table [",_TreasureTable,"] for treasure [",_TreasureID,"].");
LLLIB_Treasure_Internal_MarkAsFinished(_ID, _Object, _TreasureID, _TreasureTable);
DB_LLLIB_Treasure_TableWasGenerated(_Object, _TreasureID, _TreasureTable);
//END_REGION

//REGION GENERATE_ITEMS
PROC
LLLIB_Treasure_GenerateItems((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID)
THEN
LLLIB_Treasure_Internal_ProcessItemEntry(_ID, _Object, _TreasureID, _ItemEntry, _Amount, _RequirementID, _GenerationType, 0);

PROC
LLLIB_Treasure_GenerateItems((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID)
THEN
LLLIB_Treasure_Internal_ProcessItemEntry(_ID, _Object, _TreasureID, _ItemEntry, _Amount, _RequirementID, _GenerationType, 1);

PROC
LLLIB_Treasure_GenerateItems((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
AND
NOT DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _, _, _)
AND
NOT DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _, _, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:GenerateItems] Skipping generation for items with [",_ID,"]: No entries found for Treasure ID [",_TreasureID,"].");
LLLIB_Treasure_Internal_OnGenerationFinished(_ID, _Object);
//LLLIB_Treasure_Internal_OnGenerationFailed(_ID, _Object, _TreasureID, _GenerationType);

PROC
LLLIB_Treasure_Internal_ProcessItemEntry((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID, (STRING)_GenerationType, (INTEGER)_IsStat)
AND
LLLIB_QRY_Treasure_ItemCanGenerate(_TreasureID, _ItemEntry, _RequirementID, _GenerationType)
THEN
LLLIB_Treasure_Internal_StartItemGeneration(_ID, _Object, _TreasureID, _ItemEntry, _Amount, _RequirementID, _IsStat);

PROC
LLLIB_Treasure_Internal_ProcessItemEntry((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID, (STRING)_GenerationType, (INTEGER)_IsStat)
AND
NOT LLLIB_QRY_Treasure_ItemCanGenerate(_TreasureID, _ItemEntry, _RequirementID, _GenerationType)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:ProcessItemEntry] Failed to generate items for [",_ID,"]. Treasure [",_TreasureID,"] does not pass the requirements.");
LLLIB_Treasure_Internal_OnGenerationFinished(_ID, _Object);

PROC
LLLIB_Treasure_Internal_StartItemGeneration((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID, (INTEGER)_IsStat)
AND
LLLIB_QRY_Treasure_SkipAmountCheck(_Object, _RequirementID)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:GenerateItems] Skipping amount check and generating item [",_TreasureID,"]:[",_ItemEntry,"].");
ObjectSetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 0);
DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, 0, _Amount, _IsStat);

PROC
LLLIB_Treasure_Internal_StartItemGeneration((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID, 0)
AND
NOT LLLIB_QRY_Treasure_SkipAmountCheck(_Object, _RequirementID)
AND
LLLIB_QRY_Treasure_CountTemplate(_Object, _TreasureID, _ItemEntry)
THEN
DB_LLLIB_Treasure_Temp_Queue_CountTemplate(_Object, _TreasureID, _ItemEntry, _Amount, _RequirementID);

PROC
LLLIB_Treasure_Internal_StartItemGeneration((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID, 1)
AND
NOT LLLIB_QRY_Treasure_SkipAmountCheck(_Object, _RequirementID)
AND
NOT DB_LLLIB_Treasure_StatToTemplate(_ItemEntry, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Treasure:StartItemGeneration] Object is missing stat-based [",_TreasureID,"]:[",_ItemEntry,"] item template. Skipping counting.");
ObjectSetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 0);
DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, 0, _Amount, 1);

PROC
LLLIB_Treasure_Internal_StartItemGeneration((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID, 1)
AND
NOT LLLIB_QRY_Treasure_SkipAmountCheck(_Object, _RequirementID)
AND
DB_LLLIB_Treasure_StatToTemplate(_ItemEntry, _ItemTemplate)
AND
LLLIB_QRY_Treasure_CountTemplateWithStat(_Object, _TreasureID, _ItemTemplate, _ItemEntry)
THEN
DB_LLLIB_Treasure_Temp_Queue_CountStat(_Object, _TreasureID, _ItemTemplate, _ItemEntry, _Amount, _RequirementID);

PROC
LLLIB_Treasure_Internal_StartItemGeneration((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_Amount, (STRING)_RequirementID, (INTEGER)_IsStat)
AND
LLLIB_QRY_Treasure_CountedItems(_Object, _TreasureID, _ItemEntry)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:Treasure:StartItemGeneration] Object counting complete [",_TreasureID,"]:[",_ItemEntry,"].");
LLLIB_Treasure_Internal_CountingComplete(_ID, _Object, _TreasureID, _ItemEntry);

IF
DB_LLLIB_Treasure_Temp_GenerateItem((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_CreatedAmount, (INTEGER)_MaxAmount, (INTEGER)_IsStat)
THEN
LLLIB_Treasure_Internal_GenerateItemNow(_ID, _Object, _TreasureID, _ItemEntry, _CreatedAmount, _MaxAmount, _IsStat);

PROC
LLLIB_Treasure_Internal_GenerateItemNow((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_CreatedAmount, (INTEGER)_MaxAmount, (INTEGER)_IsStat)
AND
_IsStat <= 0
AND
_CreatedAmount < _MaxAmount
AND
IntegerSum(_CreatedAmount, 1, _NextAmount)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:GenerateItems] Generating item [",_TreasureID,"]:[",_ItemEntry,"].");
ItemTemplateAddTo(_ItemEntry, _Object, 1);
NOT DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, _CreatedAmount, _MaxAmount, _IsStat);
DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, _NextAmount, _MaxAmount, _IsStat);

PROC
LLLIB_Treasure_Internal_GenerateItemNow((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_CreatedAmount, (INTEGER)_MaxAmount, (INTEGER)_IsStat)
AND
_IsStat > 0
AND
_CreatedAmount < _MaxAmount
AND
IntegerSum(_CreatedAmount, 1, _NextAmount)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:GenerateItems] Generating stat item [",_TreasureID,"]:[",_ItemEntry,"].");
LLLIB_Treasure_Internal_GenerateItemByStat(_ID, _Object, _TreasureID, _ItemEntry);
NOT DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, _CreatedAmount, _MaxAmount, _IsStat);
DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, _NextAmount, _MaxAmount, _IsStat);

PROC
LLLIB_Treasure_Internal_GenerateItemNow((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_CreatedAmount, (INTEGER)_MaxAmount, (INTEGER)_IsStat)
AND
_CreatedAmount >= _MaxAmount
AND
_IsStat <= 0
THEN
LLLIB_Treasure_Internal_MarkAsFinished(_ID, _Object, _TreasureID, _ItemEntry);

IF
DB_LLLIB_Treasure_Temp_GenerateItem((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_CreatedAmount, (INTEGER)_MaxAmount, (INTEGER)_IsStat)
AND
_CreatedAmount >= _MaxAmount
THEN
NOT DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, _CreatedAmount, _MaxAmount, _IsStat);
//END_REGION

//REGION GENERATION_QUEUE
PROC
LLLIB_Treasure_AddToGenerationQueue((GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
AND
NOT DB_LLLIB_Treasure_Temp_ItemGenerationQueue(_, _Object, _TreasureID, _GenerationType)
AND
Random(999999, _Ran)
AND
IntegertoString(_Ran, _RanStr)
AND
GetUUID(_Object, _ObjectID)
AND
LeaderLog_QRY_Log("COMBINE", _ObjectID, "_", _TreasureID, "_", _GenerationType, "_", _RanStr)
AND
DB_LeaderLog_Temp_CombinedString(_ID)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_ID);
LLLIB_Array_AddToArray("LLLIB_ItemGenerationQueue", _ID);
DB_LLLIB_Treasure_Temp_ItemGenerationQueue(_ID, _Object, _TreasureID, _GenerationType);


IF
DB_LLLIB_Treasure_Temp_ItemGenerationQueue(_ID, _Object, _TreasureID, _GenerationType)
AND
GlobalGetFlag("LeaderLib_DefaultEventFlowRunning", 0)
AND
NOT DB_LLLIB_Treasure_Temp_QueueTimerActive(1,_)
THEN
LeaderLib_SendEvent("LeaderLib_Commands_StartTreasureGenerationQueue");

IF
StoryEvent(_, "LeaderLib_Commands_StartTreasureGenerationQueue")
THEN
LLLIB_Treasure_StartQueue(50);

PROC
LLLIB_Treasure_StartQueue((INTEGER)_TickRate)
AND
DB_LLLIB_Treasure_Temp_QueueTimerActive(1, _CurrentTickRate)
THEN
NOT DB_LLLIB_Treasure_Temp_QueueTimerActive(1, _CurrentTickRate);
DB_LLLIB_Treasure_Temp_QueueTimerActive(1, _TickRate);
TimerCancel("LLLIB_Timers_Treasure_ItemQueue");
TimerLaunch("LLLIB_Timers_Treasure_ItemQueue", _TickRate);

PROC
LLLIB_Treasure_StartQueue((INTEGER)_TickRate)
AND
NOT DB_LLLIB_Treasure_Temp_QueueTimerActive(_,_)
THEN
DB_LLLIB_Treasure_Temp_QueueTimerActive(1, _TickRate);
TimerLaunch("LLLIB_Timers_Treasure_ItemQueue", _TickRate);

IF
TimerFinished("LLLIB_Timers_Treasure_ItemQueue")
AND
LLLIB_Array_QRY_Pop("LLLIB_ItemGenerationQueue")
AND
DB_LLLIB_Array_Temp_PopValue("LLLIB_ItemGenerationQueue", _ID)
THEN
NOT DB_LLLIB_Array_Temp_PopValue("LLLIB_ItemGenerationQueue", _ID);
LLLIB_Treasure_Internal_ProcessQueueItem(_ID);

PROC
LLLIB_Treasure_Internal_StartTimeoutTimer((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
AND
NOT DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_,_,_,_)
THEN
DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType);
TimerLaunch("LLLIB_Timers_Treasure_TimeoutTimer", 5000);

PROC
LLLIB_Treasure_Internal_CancelTimeoutTimer((STRING)_ID)
AND
DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType)
THEN
NOT DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType);
TimerCancel("LLLIB_Timers_Treasure_TimeoutTimer");

PROC
LLLIB_Treasure_Internal_CancelTimeoutTimer((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
AND
DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType)
THEN
NOT DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType);
TimerCancel("LLLIB_Timers_Treasure_TimeoutTimer");

IF
TimerFinished("LLLIB_Timers_Treasure_TimeoutTimer")
AND
DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType)
THEN
NOT DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType);
LLLIB_Treasure_Internal_OnGenerationFailed(_ID, _Object, _TreasureID, _GenerationType);

PROC
LLLIB_Treasure_Internal_ProcessQueueItem((STRING)_ID)
AND
DB_LLLIB_Treasure_Temp_ItemGenerationQueue(_ID, _Object, _TreasureID, _GenerationType)
THEN
LLLIB_Treasure_Internal_StartTimeoutTimer(_ID, _Object, _TreasureID, _GenerationType);
NOT DB_LLLIB_Treasure_Temp_ItemGenerationQueue(_ID, _Object, _TreasureID, _GenerationType);
LLLIB_Treasure_GenerateItems(_ID, _Object, _TreasureID, _GenerationType);

PROC
LLLIB_Treasure_Internal_MarkAsFinished((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry)
THEN
LLLIB_Treasure_SendGeneratedEvent(_Object, _TreasureID, _ItemEntry);
LLLIB_Treasure_RemoveIfOnlyOnce(_Object, _TreasureID, _ItemEntry);

QRY
LLLIB_Treasure_QRY_StillInQueue((GUIDSTRING)_Object)
AND
DB_LLLIB_Treasure_Temp_ItemGenerationQueue(_, _Object, _, _)
THEN
DB_NOOP(1);

PROC
LLLIB_Treasure_Internal_MarkAsFinished((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
NOT LLLIB_Treasure_QRY_StillInQueue(_Object)
THEN
ObjectSetFlag(_Object, "LLLIB_Treasure_GenerationSuccessful", 0);
LLLIB_Treasure_Internal_OnGenerationFinished(_ID, _Object);

PROC
LLLIB_Treasure_Internal_OnGenerationFinished((STRING)_ID, (GUIDSTRING)_Object)
THEN
LLLIB_Treasure_Internal_CancelTimeoutTimer(_ID);
LLLIB_Treasure_Internal_GenerationQueue_NextTick(_ID);

PROC
LLLIB_Treasure_Internal_OnGenerationFailed((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_GenerationType)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:OnGenerationFailed] Failed to generate items for [",_ID,"]. Going to next item in queue.");
LLLIB_Treasure_Internal_CancelTimeoutTimer(_ID, _Object, _TreasureID, _GenerationType);
LLLIB_Treasure_Internal_GenerationQueue_NextTick(_ID);

PROC
LLLIB_Treasure_Internal_GenerationQueue_NextTick((STRING)_ID)
AND
DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID)
THEN
NOT DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID);
TimerCancel("LLLIB_Timers_Treasure_TimeoutTimer");

PROC
LLLIB_Treasure_Internal_GenerationQueue_NextTick((STRING)_ID)
AND
DB_LLLIB_Array_Length("LLLIB_ItemGenerationQueue", _Length)
AND
_Length > 0
AND
DB_LLLIB_Treasure_Temp_QueueTimerActive(_Active, _TickRate)
THEN
NOT DB_LLLIB_Treasure_Temp_QueueTimerActive(_Active, _TickRate);
LLLIB_Treasure_StartQueue(_TickRate);

PROC
LLLIB_Treasure_Internal_GenerationQueue_NextTick((STRING)_ID)
AND
DB_LLLIB_Array_Length("LLLIB_ItemGenerationQueue", _Length)
AND
_Length <= 0
AND
DB_LLLIB_Treasure_Temp_QueueTimerActive(_Active, _TickRate)
THEN
NOT DB_LLLIB_Treasure_Temp_QueueTimerActive(_Active, _TickRate);
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:OnGenerationFinished] Item generation queue complete.");
LeaderLib_SendEvent("LeaderLib_Events_OnTreasureQueueComplete");
//END_REGION

//REGION FLAG_RESET
PROC
LLLIB_Treasure_Internal_OnGenerationFinished((STRING)_ID, (GUIDSTRING)_Object)
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_GenerationSuccessful", 1)
AND
GetUUID(_Object, _ObjectID)
AND
StringConcatenate("LLLIB_Treasure_ResetGeneratingFlagTimer_", _ObjectID, _TimerName)
AND
NOT DB_LLLIB_Treasure_Temp_ResetGeneratedFlagTimer(_Object, _TimerName)
THEN
DB_LLLIB_Treasure_Temp_ResetGeneratedFlagTimer(_Object, _TimerName);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, 500);

IF
TimerFinished(_TimerName)
AND
DB_LLLIB_Treasure_Temp_ResetGeneratedFlagTimer(_Object, _TimerName)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

IF
TimerFinished(_TimerName)
AND
DB_LLLIB_Treasure_Temp_ResetGeneratedFlagTimer(_Object, _TimerName)
THEN
ObjectClearFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure");
NOT DB_LLLIB_Treasure_Temp_ResetGeneratedFlagTimer(_Object, _TimerName);
SetStoryEvent(_Object, "LeaderLib_Events_OnGenerationComplete");
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:ResetGeneratedFlagTimer] Flag and database Temp_MatchedTreasure cleared.");
//END_REGION

//REGION ITEM_AMOUNT_COUNTING
PROC
LLLIB_Treasure_Internal_CountingComplete((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_Temp_Queue_CountTemplate(_Object, _TreasureID, _ItemEntry, _GeneratedAmount, _RequirementID)
AND
DB_LLLIB_Treasure_Temp_ItemTemplateAmount(_Object, _TreasureID, _ItemEntry, _CurrentAmount)
AND
LLLIB_QRY_Treasure_Debug_CompareAmounts(_TreasureID, _ItemEntry, _GeneratedAmount, _CurrentAmount)
AND
LLLIB_QRY_Treasure_ItemLimitNotMet(_Object, _TreasureID, _ItemEntry, _RequirementID, _GeneratedAmount, _CurrentAmount)
AND
DB_LLLIB_Treasure_Temp_AmountToCreate(_Object, _ItemEntry, _AmountToCreate)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:CountingComplete] Counting complete. Generating item by template.");
ObjectSetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 0);
DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, 0, _AmountToCreate, 0);
NOT DB_LLLIB_Treasure_Temp_AmountToCreate(_Object, _ItemEntry, _AmountToCreate);
NOT DB_LLLIB_Treasure_Temp_ItemTemplateAmount(_Object, _TreasureID, _ItemEntry, _CurrentAmount);
NOT DB_LLLIB_Treasure_Temp_Queue_GenerateTemplateItem(_Object, _TreasureID, _ItemEntry, _GeneratedAmount, _RequirementID);

PROC
LLLIB_Treasure_Internal_CountingComplete((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_Temp_Queue_CountStat(_Object, _TreasureID, _ItemTemplate, _ItemEntry, _GeneratedAmount, _RequirementID)
AND
DB_LLLIB_Treasure_Temp_ItemStatAmount(_Object, _TreasureID, _ItemTemplate, _ItemEntry, _CurrentAmount)
AND
LLLIB_QRY_Treasure_ItemLimitNotMet(_Object, _TreasureID, _ItemEntry, _RequirementID, _GeneratedAmount, _CurrentAmount)
AND
DB_LLLIB_Treasure_Temp_AmountToCreate(_Object, _ItemEntry, _AmountToCreate)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:CountingComplete] Counting complete. Generating item by stat.");
ObjectSetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 0);
DB_LLLIB_Treasure_Temp_GenerateItem(_ID, _Object, _TreasureID, _ItemEntry, 0, _AmountToCreate, 1);
NOT DB_LLLIB_Treasure_Temp_AmountToCreate(_Object, _ItemEntry, _AmountToCreate);
NOT DB_LLLIB_Treasure_Temp_ItemStatAmount(_Object, _TreasureID, _ItemTemplate, _ItemEntry, _CurrentAmount);
NOT DB_LLLIB_Treasure_Temp_Queue_GenerateStatItem(_Object, _TreasureID, _ItemEntry, _ItemTemplate, _GeneratedAmount, _RequirementID);
//END_REGION

//REGION GENERATING_BY_STAT
PROC
LLLIB_Treasure_Internal_GenerateItemByStat((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
GetUUID(_Object, _ObjectID)
AND
LeaderLog_QRY_Log("COMBINE", _ObjectID, "_", _TreasureID, "_", _ItemEntry)
AND
DB_LeaderLog_Temp_CombinedString(_DummyID)
AND
LeaderLog_QRY_ClearCombinedString(_DummyID)
AND
LLLIB_QRY_Treasure_PrepareGeneratorDummy(_DummyID)
AND
DB_LLLIB_Treasure_NextGeneratorDummy(_Dummy, _DummyID)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:StatItemGen] Creating item by stat [",_ItemEntry,"] using Dummy ID [",_DummyID,"].");
DB_LLLIB_Treasure_Temp_DummyTarget(_Object, _Dummy, _TreasureID, _ItemEntry, _DummyID);
//SetVarObject(_Dummy, "LLLIB_Treasure_ObjectTarget", _Object);
SetVarFixedString(_Dummy, "LeaderLib_CreateByStat_NextStatToCreate", _ItemEntry);
SetVarFixedString(_Dummy, "LeaderLib_CreateByStat_FlagToSet", "LLLIB_GeneratorDummy_GeneratedItem");
SetStoryEvent(_Dummy, "LeaderLib_Commands_CreateItemByStat");

PROC
LLLIB_Treasure_Internal_GenerateItemByStat((STRING)_ID, (GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
NOT DB_LLLIB_Treasure_GeneratorDummies(_)
AND
DB_LLLIB_Treasure_Temp_TimeoutTimerActive(_ID, _Object, _TreasureID, _GenerationType)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:StatItemGen][ERROR] Error creating item by stat: No generator dummy found!");
LLLIB_Treasure_Internal_OnGenerationFailed(_ID, _Object, _TreasureID, _GenerationType);

IF
ItemAddedToCharacter(_Item, _Dummy)
AND
GetTemplate(_Dummy, "LLLIB_GeneratorDummy_24a5d60b-680b-4fbc-9b2c-19061a261297")
AND
ObjectGetFlag(_Dummy, "LLLIB_GeneratorDummy_GeneratedItem", 1)
AND
GetVarString(_Dummy, "LLLIB_DummyID", _DummyID)
AND
DB_LLLIB_Treasure_Temp_DummyTarget(_Object, _Dummy, _TreasureID, _ItemEntry, _DummyID)
AND
GetTemplate(_Item, _ItemTemplate)
AND
GetVarFixedString(_Dummy, "LeaderLib_CreateByStat_NextStatToCreate", _ItemEntry)
AND
ItemGetAmount(_Item, _Amount)
AND
DB_LLLIB_Treasure_Temp_ItemGenerationQueue(_ID, _Object, _TreasureID, _GenerationType)
THEN
NOT DB_LLLIB_Treasure_Temp_DummyTarget(_Object, _Dummy, _TreasureID, _ItemEntry, _DummyID);
LLLIB_Treasure_ResetDummy(_Dummy);
SetVarFixedString(_Dummy, "LeaderLib_CreateByStat_NextStatToCreate", "");
ObjectClearFlag(_Dummy, "LLLIB_GeneratorDummy_GeneratedItem");
ItemToInventory(_Item, _Object, _Amount, 0, 1);
SetVarFixedString(_Item, "LeaderLib_GeneratedItemStat", _ItemEntry);
LLLIB_Treasure_Internal_MarkAsFinished(_ID, _Object, _TreasureID, _ItemEntry);
//DB_LLLIB_Treasure_StatToTemplate(_ItemEntry, _ItemTemplate);
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:StatItem] Stat item [",_ItemEntry,"] sent to target.");
//END_REGION

//REGION POST_GENERATION
IF
ItemAddedToCharacter(_Item, _Object)
AND
LLLIB_QRY_Treasure_GetTreasureID((GUIDSTRING)_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
LLLIB_Treasure_OnAdded(_Object, _Item, _TreasureID);

IF
ItemAddedToContainer(_Item, _Object)
AND
LLLIB_QRY_Treasure_GetTreasureID((GUIDSTRING)_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
LLLIB_Treasure_OnAdded(_Object, _Item, _TreasureID);

IF
ItemAddedToContainer(_Item, _Object)
AND
GetVarFixedString(_Item, "LeaderLib_GeneratedItemStat", _ItemEntry)
AND
GetStatString(_Item, _Stat)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:OnAdded(Debug)] Item with saved stat ",_ItemEntry,"(",_Stat,") added to container.");

PROC
LLLIB_Treasure_OnAdded((GUIDSTRING)_Object, (ITEMGUID)_Item, (STRING)_TreasureID)
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 1)
AND
GetTemplate(_Item, _ItemEntry)
AND
DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID)
THEN
LLLIB_Treasure_AdjustItemLevel(_Item, _TreasureID, _ItemEntry);
LLLIB_Treasure_ApplyDeltaMods(_Item, _TreasureID, _ItemEntry);
LLLIB_Treasure_InsertRunes(_Item, _TreasureID, _ItemEntry);
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:OnAdded(TemplateItem] Adjusted treasure level, delta mods, and runes.");

PROC
LLLIB_Treasure_OnAdded((GUIDSTRING)_Object, (ITEMGUID)_Item, (STRING)_TreasureID)
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 1)
AND
GetVarFixedString(_Item, "LeaderLib_GeneratedItemStat", _ItemEntry)
AND
DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID)
AND
GetTemplate(_Item, _ItemTemplate)
THEN
//Link the stat to the template for easier amount retrieval later, hopefully.
DB_LLLIB_Treasure_StatToTemplate(_ItemEntry, _ItemTemplate);
LLLIB_Treasure_AdjustItemLevel(_Item, _TreasureID, _ItemEntry);
LLLIB_Treasure_ApplyDeltaMods(_Item, _TreasureID, _ItemEntry);
LLLIB_Treasure_InsertRunes(_Item, _TreasureID, _ItemEntry);
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:OnAdded(StatItem)] Adjusted treasure level, delta mods, and runes.");

PROC
LLLIB_Treasure_OnAdded((GUIDSTRING)_Object, (ITEMGUID)_Item, (STRING)_TreasureID)
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 1)
AND
GetVarFixedString(_Item, "LeaderLib_GeneratedItemStat", _ItemEntry)
AND
DB_LLLIB_Treasure_TreasureItemStats(_TreasureID, _ItemEntry, _Amount, _RequirementID)
AND
NOT GetTemplate(_Item, _)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:OnAdded(StatItem)][ERROR] Couldn't find a template for item with stat ", _ItemEntry);

/*
PROC
LLLIB_Treasure_OnAdded((GUIDSTRING)_Object, (ITEMGUID)_Item)
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 1)
THEN
//LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem] Treasure added.");
*/

PROC
LLLIB_Treasure_SendGeneratedEvent((GUIDSTRING)_Owner, (STRING)_OwnerID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_GeneratedEvent(_OwnerID, _ItemEntry, _OnGeneratedEvent)
THEN
SetStoryEvent(_Owner, _OnGeneratedEvent);

PROC
LLLIB_Treasure_RemoveIfOnlyOnce((GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_OnlyOnce(_TreasureID, _ItemEntry, _OnCompletionEvent)
AND
DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID)
THEN
NOT DB_LLLIB_Treasure_TreasureItemTemplates(_TreasureID, _ItemEntry, _Amount, _RequirementID);

PROC
LLLIB_Treasure_RemoveIfOnlyOnce((GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_OnlyOnce(_TreasureID, _ItemEntry, _OnCompletionEvent)
THEN
ObjectSetFlag(_Object, "LLLIB_Treasure_GenerationEnded");
SetStoryEvent(_Object, _OnCompletionEvent);

PROC
LLLIB_Treasure_DisableGenerationForObject((GUIDSTRING)_Object)
AND
IsTagged(_Object, "LLLIB_Treasure_GenerateEndlessly", 0)
THEN
ObjectSetFlag(_Object, "LLLIB_Treasure_GenerationEnded");

IF
ObjectWasTagged(_Object, "LLLIB_Treasure_GenerateEndlessly")
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_GenerationEnded", 1)
THEN
ObjectClearFlag(_Object, "LLLIB_Treasure_GenerationEnded");
//END_REGION

//REGION ITEM_POSTGEN_MODIFIERS
PROC
LLLIB_Treasure_AdjustItemLevel((ITEMGUID)_Item, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_ItemGenerationSettings(_TreasureID, _ItemEntry, _UsePartyLevel, _MinLevel, _MaxLevel)
AND
_UsePartyLevel > 0
AND
LLLIB_Helper_QRY_GetHighestLevelInParty()
AND
DB_LLLIB_Helper_Temp_HighestLevelInParty(_Level)
THEN
ItemLevelUpTo(_Item, _Level);

PROC
LLLIB_Treasure_AdjustItemLevel((ITEMGUID)_Item, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_ItemGenerationSettings(_TreasureID, _ItemEntry, _UsePartyLevel, _MinLevel, _MaxLevel)
AND
_UsePartyLevel <= 0
AND
LLLIB_Random(_MinLevel, _MaxLevel)
AND
DB_LLLIB_Temp_RandomResult(_Level)
THEN
ItemLevelUpTo(_Item, _Level);
NOT DB_LLLIB_Temp_RandomResult(_Level);

PROC
LLLIB_Treasure_ApplyDeltaMods((ITEMGUID)_Item, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_ItemDeltaMods(_TreasureID, _ItemEntry, _Deltamod, _Chance)
AND
NOT ItemHasDeltaModifier(_Item, _Deltamod, _)
AND
LLLIB_Random(100)
AND
DB_LLLIB_Temp_RandomResult(_Roll)
AND
_Roll <= _Chance
THEN
ItemAddDeltaModifier(_Item, _Deltamod);
NOT DB_LLLIB_Temp_RandomResult(_Roll);

PROC
LLLIB_Treasure_InsertRunes((ITEMGUID)_Item, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
NOT DB_LLLIB_Treaasure_Temp_RuneSlot(_Item,_)
THEN
DB_LLLIB_Treaasure_Temp_RuneSlot(_Item, 0);

PROC
LLLIB_Treasure_InsertRunes((ITEMGUID)_Item, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treasure_ItemRunes(_TreasureID, _ItemEntry, _Rune, _Chance)
AND
DB_LLLIB_Treaasure_Temp_RuneSlot(_Item, _Slot)
AND
LLLIB_Treasure_QRY_InsertRuneInSlot(_Item, _Rune, _Slot, _Chance)
AND
IntegerSum(_Slot, 1, _NextSlot)
THEN
NOT DB_LLLIB_Treaasure_Temp_RuneSlot(_Item, _Slot);
DB_LLLIB_Treaasure_Temp_RuneSlot(_Item, _NextSlot);

QRY
LLLIB_Treasure_QRY_InsertRuneInSlot((ITEMGUID)_Item, (STRING)_Rune, (INTEGER)_Slot, (INTEGER)_Chance)
AND
NOT ItemGetRuneItemTemplate(_Item, _Slot, _)
AND
LLLIB_Random(100)
AND
DB_LLLIB_Temp_RandomResult(_Roll)
AND
_Roll <= _Chance
THEN
LLLIB_Treasure_Internal_DummyInsertRune(_Item, _Rune, _Slot);
NOT DB_LLLIB_Temp_RandomResult(_Roll);

PROC
LLLIB_Treasure_Internal_DummyInsertRune((ITEMGUID)_Item, (STRING)_Rune, (INTEGER)_Slot)
AND
DB_LLLIB_Treasure_GeneratorDummies(_Dummy)
THEN
ItemInsertRune((CHARACTERGUID)_Dummy, _Item, _Rune, _Slot);

PROC
LLLIB_Treasure_InsertRunes((ITEMGUID)_Item, (STRING)_TreasureID, (STRING)_ItemEntry)
AND
DB_LLLIB_Treaasure_Temp_RuneSlot(_Item, _Slot)
THEN
NOT DB_LLLIB_Treaasure_Temp_RuneSlot(_Item, _Slot);
//END_REGION

//REGION GENERATION_EVENTS
//Containers
IF
//ItemOpened(_Object)
CharacterUsedItem(_, _Object)
AND
IsTagged(_Object, "LLLIB_Item_IgnoreOpened", 0)
AND
LLLIB_QRY_Treasure_ObjectCanStartGeneration((GUIDSTRING)_Object)
AND
ItemIsContainer((ITEMGUID)_Object, 1)
AND
LLLIB_QRY_Treasure_GetTreasureID((GUIDSTRING)_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
//AND
//Don't check the IteMGenerationType database since this is the default way for containers
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "OPENED");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

IF
ObjectFlagSet("LeaderLib_Treasure_GenerateNow", _Object, _)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Object, "LeaderLib_Treasure_GenerateNow")
AND
ObjectGetFlag(_Object, "LLLIB_Treasure_IsGeneratingTreasure", 0)
AND
LLLIB_QRY_Treasure_GetTreasureID(_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem:GenerateNow] Manually generating items for treasure with ID [",_TreasureID,"]");
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "MANUAL");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

IF
TradeGenerationStarted(_Object)
AND
DB_LLLIB_Treasure_RegisteredGenerationTypes("TRADE_GENERATION_START")
AND
IsTagged(_Object, "LLLIB_Trader_IgnoreTradeGeneration", 0)
AND
LLLIB_QRY_Treasure_ObjectCanStartGeneration((GUIDSTRING)_Object)
AND
LLLIB_QRY_Treasure_GetTreasureID(_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
AND
LLLIB_QRY_Treasure_TreasureHasGenerationType(_TreasureID, "TRADE_GENERATION_START")
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "TRADE_GENERATION_START");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

IF
TradeGenerationEnded(_Object)
AND
IsTagged(_Object, "LLLIB_Trader_IgnoreTradeGeneration", 0)
AND
LLLIB_QRY_Treasure_ObjectCanStartGeneration((GUIDSTRING)_Object)
AND
LLLIB_QRY_Treasure_GetTreasureID(_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
//AND
//Skip this check since it's the default generation for traders
//LLLIB_QRY_Treasure_TreasureHasGenerationType(_TreasureID, "TRADE_GENERATION_END")
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "TRADE_GENERATION_END");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

IF
DialogStarted(_Dialog, _Instance)
AND
DB_LLLIB_Treasure_RegisteredGenerationTypes("DIALOG_STARTED")
AND
DialogGetInvolvedNPC(_Instance, 1, _Object)
AND
IsTagged(_Object, "LLLIB_Trader_GenerateOnDialog", 1)
AND
LLLIB_QRY_Treasure_ObjectCanStartGeneration((GUIDSTRING)_Object)
AND
LLLIB_QRY_Treasure_GetTreasureID(_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
AND
LLLIB_QRY_Treasure_TreasureHasGenerationType(_TreasureID, "DIALOG_STARTED")
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "DIALOG_STARTED");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

IF
DialogEnded(_Dialog, _Instance)
AND
DB_LLLIB_Treasure_RegisteredGenerationTypes("DIALOG_ENDED")
AND
DialogGetInvolvedNPC(_Instance, 1, _Object)
AND
IsTagged(_Object, "LLLIB_Trader_GenerateOnDialog", 1)
AND
LLLIB_QRY_Treasure_ObjectCanStartGeneration(_Object)
AND
LLLIB_QRY_Treasure_GetTreasureID(_Object)
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
AND
LLLIB_QRY_Treasure_TreasureHasGenerationType(_TreasureID, "DIALOG_ENDED")
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "DIALOG_ENDED");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

IF
ObjectFlagSet(_Flag, _, _)
THEN
LLLIB_Treasure_Internal_GenerateByFlag(_Flag, "Object");

IF
GlobalFlagSet(_Flag)
THEN
LLLIB_Treasure_Internal_GenerateByFlag(_Flag, "Global");

IF
GameStarted(_,_)
THEN
//Give some time for other mods to register treasure
TimerLaunch("LLLIB_Timers_Treasure_LevelLoadedTreasureTimer", 2000);

IF
TimerFinished("LLLIB_Timers_Treasure_LevelLoadedTreasureTimer")
AND
DB_LLLIB_Treasure_RegisteredGenerationTypes("LEVEL_LOADED")
AND
LLLIB_QRY_Treasure_GetTreasureByGenerationType("LEVEL_LOADED")
AND
DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure("LEVEL_LOADED", _TreasureID)
AND
LLLIB_QRY_Treasure_GetTreasureObject(_TreasureID, "LEVEL_LOADED")
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "LEVEL_LOADED");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);
NOT DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure("LEVEL_LOADED", _TreasureID);

IF
CharacterLeveledUp(_Player)
AND
CharacterIsPlayer(_Player, 1)
THEN
TimerCancel("LLLIB_Timers_Treasure_LevelUpTreasureTimer");
TimerLaunch("LLLIB_Timers_Treasure_LevelUpTreasureTimer", 1000);

IF
TimerFinished("LLLIB_Timers_Treasure_LevelUpTreasureTimer")
AND
DB_LLLIB_Treasure_RegisteredGenerationTypes("PARTY_LEVELED_UP")
AND
LLLIB_QRY_Treasure_GetTreasureByGenerationType("PARTY_LEVELED_UP")
AND
DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure("PARTY_LEVELED_UP", _TreasureID)
AND
LLLIB_QRY_Treasure_GetTreasureObject(_TreasureID, "PARTY_LEVELED_UP")
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
AND
NOT DB_LLLIB_Treasure_Temp_PartyLeveledUpGenerationTimer(_, _TreasureID)
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "PARTY_LEVELED_UP");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);
NOT DB_LLLIB_Treasure_Temp_GenerationTypeToTreasure("PARTY_LEVELED_UP", _TreasureID);

//When an object is found by its registered treasure ID and template
IF
StoryEvent(_, "LLLIB_Events_Treasure_ContinueGeneration")
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
AND
DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template, _GenerationType)
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, _GenerationType);
NOT DB_LLLIB_Treasure_Temp_MatchTemplate(_TreasureID, _Template, _GenerationType);
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

//To prevent multiple generation events when more than one player levels up at the same time
PROC
LLLIB_Treasure_AddToGenerationQueue((GUIDSTRING)_Object, (STRING)_TreasureID, "PARTY_LEVELED_UP")
AND
NOT DB_LLLIB_Treasure_Temp_PartyLeveledUpGenerationTimer(_, _TreasureID)
AND
StringConcatenate("LLLIB_Timers_Treasure_PartyLeveledUpGenerationBlocker_", _TreasureID, _TimerName)
THEN
DB_LLLIB_Treasure_Temp_PartyLeveledUpGenerationTimer(_TimerName, _TreasureID);
TimerLaunch(_TimerName, 2000);

IF
TimerFinished(_TimerName)
AND
DB_LLLIB_Treasure_Temp_PartyLeveledUpGenerationTimer(_TimerName, _TreasureID)
THEN
NOT DB_LLLIB_Treasure_Temp_PartyLeveledUpGenerationTimer(_TimerName, _TreasureID);

//Use a proc here to avoid a weird crash
IF
DB_LLLIB_Requirements_JustUnlocked(_RequirementID)
THEN
LLLIB_Treasure_Internal_GenerateFromRequirement(_RequirementID);

PROC
LLLIB_Treasure_Internal_GenerateFromRequirement((STRING)_RequirementID)
AND
DB_LLLIB_Treasure_RegisteredGenerationTypes("REQUIREMENT_UNLOCKED")
AND
LLLIB_QRY_Treasure_GetTreasureByRequirement(_RequirementID)
AND
DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID)
AND
LLLIB_QRY_Treasure_TreasureHasGenerationType(_TreasureID, "REQUIREMENT_UNLOCKED")
AND
LLLIB_QRY_Treasure_GetTreasureObject(_TreasureID, "REQUIREMENT_UNLOCKED")
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:TreasureSystem] Generating [",_TreasureID,"] due to requirement [",_RequirementID,"] unlocking.");
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "REQUIREMENT_UNLOCKED");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);

PROC
LLLIB_Treasure_Internal_GenerateFromRequirement((STRING)_RequirementID)
AND
DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID)
THEN
NOT DB_LLLIB_Treasure_Temp_MatchedRequirementTreasure(_TreasureID, _RequirementID);
//END_REGION

//REGION TREASURE_FLAG_GEN
QRY
LLLIB_QRY_Treasure_IsGenerationFlag((STRING)_Flag, (STRING)_FlagType)
AND
DB_LLLIB_Treasure_Temp_FlagToTreasure(_Flag, _FlagType, _TreasureID)
THEN
NOT DB_LLLIB_Treasure_Temp_FlagToTreasure(_Flag, _FlagType, _TreasureID);

QRY
LLLIB_QRY_Treasure_IsGenerationFlag((STRING)_Flag, (STRING)_FlagType)
AND
DB_LLLIB_Treasure_ItemGenerationFlag(_TreasureID, _ItemEntry, _Flag, _FlagType)
AND
NOT DB_LLLIB_Treasure_Temp_FlagToTreasure(_Flag, _FlagType, _TreasureID)
THEN
DB_LLLIB_Treasure_Temp_FlagToTreasure(_Flag, _FlagType, _TreasureID);

PROC
LLLIB_Treasure_Internal_GenerateByFlag((STRING)_Flag, (STRING)_FlagType)
AND
DB_LLLIB_Treasure_RegisteredGenerationTypes("FLAG")
AND
LLLIB_QRY_Treasure_IsGenerationFlag(_Flag, _FlagType)
AND
DB_LLLIB_Treasure_Temp_FlagToTreasure(_Flag, _FlagType, _TreasureID)
AND
LLLIB_QRY_Treasure_GetTreasureObject(_TreasureID, "FLAG")
AND
DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID)
THEN
LLLIB_Treasure_AddToGenerationQueue(_Object, _TreasureID, "FLAG");
NOT DB_LLLIB_Treasure_Temp_MatchedTreasure(_Object, _TreasureID);
//END_REGION

//REGION CONTAINERS
IF
ObjectFlagSet("LLLIB_Treasure_GenerationSuccessful", _Object, _)
AND
ItemIsContainer((ITEMGUID)_Object, 1)
THEN
LLLIB_Treasure_DisableGenerationForObject((GUIDSTRING)_Object);
ObjectClearFlag(_Object, "LLLIB_Treasure_GenerationSuccessful");
//END_REGION

//REGION DEBUG
/*
QRY
LLLIB_QRY_Treasure_Debug_CompareAmounts((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_GeneratedAmount, (INTEGER)_CurrentAmount)
AND
IntegertoString(_GeneratedAmount, _GenStr)
AND
IntegertoString(_CurrentAmount, _CurStr)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Treasure:Debug] Comparing item amounts for [",_TreasureID,":",_ItemEntry,"] - ",_CurStr,"/",_GenStr);
*/

QRY
LLLIB_QRY_Treasure_Debug_CompareAmounts((STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_GeneratedAmount, (INTEGER)_CurrentAmount)
THEN
DB_NOOP(1);

PROC
LLLIB_Treasure_CountItems((GUIDSTRING)_Object, (STRING)_TreasureID, (STRING)_ItemEntry, (INTEGER)_GeneratedAmount, (STRING)_RequirementID, (INTEGER)_IsStat)
AND
_IsStat <= 0
AND
ItemTemplateIsInCharacterInventory((CHARACTERGUID)_Object, _ItemEntry, _Val)
AND
IntegertoString(_Val, _ValStr)
AND
GetTemplate(_Object, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Treasure:CountItems] Item template [",_TreasureID,"]:[",_ItemEntry,"][",_Template,"] found = ", _ValStr);

/*
IF
StoryEvent(_Object, _Event)
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Treasure:Debug] Debugging event [",_Event,"]. It is the completion event?");
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
