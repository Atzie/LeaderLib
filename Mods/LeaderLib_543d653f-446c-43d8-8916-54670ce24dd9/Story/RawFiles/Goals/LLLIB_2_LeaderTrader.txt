Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_LeaderTrader_RegisterTrader();
//DB_LLLIB_LeaderTrader(_LeaderTrader)
//DB_LLLIB_LeaderTrader_MetPlayer(_Player)

//Settings
//DB_LLLIB_LeaderTrader_Dialog(_ReferenceName, _Dialog)
KBSECTION
//REGION REGISTER
IF
StoryEvent(_, "LLLIB_Events_UpdateDatabases")
THEN
LLLIB_LeaderTrader_RegisterTrader();

PROC
LLLIB_LeaderTrader_RegisterTrader()
AND
NOT DB_LLLIB_LeaderTrader(_)
THEN
DB_LLLIB_LeaderTrader((CHARACTERGUID)CHARACTERGUID_S_LLLIB_RetiredLeader_61ae5acc-1537-4970-82bb-d408a3334574);

PROC
LLLIB_LeaderTrader_RegisterTrader()
THEN
LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_LeaderTrader_Dialog", 2);
DB_LLLIB_LeaderTrader_Dialog("Default", "LLLIB_RetiredLeader_Default");
DB_LLLIB_LeaderTrader_Dialog("Tutorial", "LLLIB_RetiredLeader_TutorialBoat");

PROC
LLLIB_LeaderTrader_RegisterTrader()
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
LLLIB_Trader_ClearTrader("LLLIB_LeaderTrader");
LLLIB_Trader_Register_CreationEvent("LLLIB_LeaderTrader", "LLLIB_Events_OnLeaderTradeSpawned");

LLLIB_Trader_Register_GlobalTrader("LLLIB_LeaderTrader", _LeaderTrader);
LLLIB_Trader_Register_Position("LLLIB_LeaderTrader", "FJ_FortJoy_Main", 219.86, -16.79, 362.70);

LLLIB_Requirements_AddFlagRequirement("LLLIB_LeaderTrader_WindegoHasNotCastSpell", "TUT_LowerDeck_WindegoHasCastSpell", "Global", 0);
LLLIB_Requirements_AddFlagRequirement("LLLIB_LeaderTrader_PartyFoundSeekerCamp", "FTJ_SW_BeenToShelter", "Object", 1);
LLLIB_Requirements_AddFlagRequirement("LLLIB_LeaderTrader_HasMetPlayer", "LLLIB_Trader_PlayerTalkedToLeaderTrader", "Object", 1);
LLLIB_Requirements_AddFlagRequirement("LLLIB_LeaderTrader_HasNotMetPlayer", "LLLIB_Trader_PlayerTalkedToLeaderTrader", "Object", 0);

LLLIB_Trader_Register_StartingGold("LLLIB_LeaderTrader", "LeaderLib_TestLevel", 20000);
LLLIB_Trader_Register_StartingGold("LLLIB_LeaderTrader", "TUT_Tutorial_A", 200);
LLLIB_Trader_Register_StartingGold("LLLIB_LeaderTrader", "FJ_FortJoy_Main", 2000);
LLLIB_Trader_Register_StartingGold("LLLIB_LeaderTrader", "FJ_FortJoy_Main", 4000, "LLLIB_LeaderTrader_PartyFoundSeekerCamp");
LLLIB_Trader_Register_StartingGold("LLLIB_LeaderTrader", "LV_HoE_Main", 4500);
LLLIB_Trader_Register_StartingGold("LLLIB_LeaderTrader", "RC_Main", 5000);
LLLIB_Trader_Register_StartingGold("LLLIB_LeaderTrader", "CoS_Main", 10000);
LLLIB_Trader_Register_StartingGold("LLLIB_LeaderTrader", "Arx_Main", 13000);

LLLIB_Treasure_RegisterObjectByTraderID("LLLIB_LeaderTrader_ModBooks", "LLLIB_LeaderTrader");
LLLIB_Treasure_AddTreasureItemTemplate("LLLIB_LeaderTrader_ModBooks", "LLLIB_BOOK_ModSettingsMenu_646a1194-3383-47fa-a379-ebefa3d2b108", 1);
LLLIB_Treasure_Configure_GenerationType("LLLIB_LeaderTrader_ModBooks", "LLLIB_BOOK_ModSettingsMenu_646a1194-3383-47fa-a379-ebefa3d2b108", "DIALOG_STARTED");
//LLLIB_Treasure_AddTreasureItemTemplate("LLLIB_LeaderTrader_ModBooks", "CON_Potion_Fire_Res_A_e78b642c-0216-4df4-b691-a41ff4747b6a", 8);
//LLLIB_Treasure_AddTreasureItemStat("LLLIB_LeaderTrader_ModBooks", "WPN_Cheat_Sword_1H_RuneSlot2", 1);

LLLIB_LeaderTrader_RegisterDeckSeat("LeaderLib_TestLevel", ITEMGUID_LLLIB_DebugLevel_TraderChair_8468a5fd-00a4-42c2-aa15-1b1a25e46738);
LLLIB_LeaderTrader_RegisterDeckSeat("TUT_Tutorial_A", ITEMGUID_S_TUT_KitchenBench_e628d79a-8f7b-4681-a55f-57f0b42a29db, "LLLIB_LeaderTrader_WindegoHasNotCastSpell");
LLLIB_LeaderTrader_RegisterDeckSeat("LV_HoE_Main", ITEMGUID_FUR_Humans_Citz_Stool_C_005_a2a11ad5-645b-4719-bfd5-be04f32fbb2a);
LLLIB_LeaderTrader_RegisterDeckSeat("RC_Main", ITEMGUID_FUR_Humans_Citz_Stool_C_005_a2a11ad5-645b-4719-bfd5-be04f32fbb2a);
LLLIB_LeaderTrader_RegisterDeckSeat("CoS_Main", ITEMGUID_FUR_Humans_Citz_Stool_C_005_a2a11ad5-645b-4719-bfd5-be04f32fbb2a);
LLLIB_LeaderTrader_RegisterDeckSeat("Arx_Main", ITEMGUID_FUR_Humans_Citz_Stool_C_005_a2a11ad5-645b-4719-bfd5-be04f32fbb2a);

PROC
LLLIB_LeaderTrader_RegisterDeckSeat((STRING)_LevelName, (ITEMGUID)_Seat)
THEN
LLLIB_LeaderTrader_RegisterDeckSeat(_LevelName, _Seat, "");

PROC
LLLIB_LeaderTrader_RegisterDeckSeat((STRING)_LevelName, (ITEMGUID)_Seat, (STRING)_RequirementID)
THEN
LLLIB_Trader_Register_Level("LLLIB_LeaderTrader", _LevelName, _RequirementID);
LLLIB_Trader_Register_PositionObject("LLLIB_LeaderTrader", _LevelName, (GUIDSTRING)_Seat);
LLLIB_Trader_Register_Seat("LLLIB_LeaderTrader", _LevelName, (ITEMGUID)_Seat);

LLLIB_Trader_Register_Dialog("LLLIB_LeaderTrader", "", "LLLIB_RetiredLeader_Default"); // Default
LLLIB_Trader_Register_Dialog("LLLIB_LeaderTrader", "TUT_Tutorial_A", "LLLIB_RetiredLeader_TutorialBoat");
//LLLIB_Trader_Register_Dialog("LLLIB_LeaderTrader", "LeaderLib_TestLevel", "LLLIB_RetiredLeader_TutorialBoat");
//END_REGION

//REGION APPEARANCE_CHANGING
IF
DB_LLLIB_Traders_Spawned(_LeaderTrader, _TraderID, "TUT_Tutorial_A")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
CharacterTransform(_LeaderTrader, "LLLIB_RetiredLeader_Default_e4cbf1f4-4eea-457c-93a3-ded05ae3605e", 1, 1, 1, 1, 1, 1, 0);
LLLIB_Helper_CreateItemByStatAndEquip(_LeaderTrader, "ARM_Purge_UpperBody");
LLLIB_Helper_CreateItemByStatAndEquip(_LeaderTrader, "ARM_Purge_LowerBody");
LLLIB_Helper_RefreshEquipment(_LeaderTrader);
DB_TUT_LowerDeck_WindegoSpareCharacters(_LeaderTrader); // To avoid exploding

IF
DB_LLLIB_Traders_Spawned(_LeaderTrader, _TraderID, "FJ_FortJoy_Main")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
CharacterTransform(_LeaderTrader, "LLLIB_RetiredLeader_Act1_da17fca2-fe16-46d9-80b3-08971a8c8d49", 1, 1, 1, 1, 1, 1, 0);
LLLIB_Helper_RefreshEquipment(_LeaderTrader);

IF
DB_LLLIB_Traders_Spawned(_LeaderTrader, _TraderID, "LeaderLib_TestLevel")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
//CharacterTransform(_LeaderTrader, "LLLIB_RetiredLeader_Act1_da17fca2-fe16-46d9-80b3-08971a8c8d49", 1, 1, 1, 1, 1, 1, 0);
LLLIB_Helper_CreateItemByStatAndEquip(_LeaderTrader, "ARM_Purge_UpperBody", 0);
LLLIB_Helper_CreateItemByStatAndEquip(_LeaderTrader, "ARM_Purge_LowerBody", 0);
//LLLIB_Helper_RefreshEquipment(_LeaderTrader);
//END_REGION

//REGION TUTORIAL
IF
GlobalFlagSet("TUT_LowerDeck_WindegoHasCastSpell")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_LLLIB_Traders_Spawned(_LeaderTrader, _TraderID, _Level)
THEN
DisplayText(_LeaderTrader, "Hrmph. Sounds like my cue to sneak on out of here...");
CharacterUseSkill(_LeaderTrader, "Shout_ChameleonSkin", _LeaderTrader, 0, 1, 1);
TimerLaunch("LLLIB_Timeers_LeaderTrader_TUTDisappear", 2000);

IF
TimerFinished("LLLIB_Timeers_LeaderTrader_TUTDisappear")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_LLLIB_Traders_Spawned(_LeaderTrader, _TraderID, "TUT_Tutorial_A")
THEN
NOT DB_LLLIB_Traders_Spawned(_LeaderTrader, _TraderID, "TUT_Tutorial_A");

IF
TimerFinished("LLLIB_Timeers_LeaderTrader_TUTDisappear")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
SetOnStage(_LeaderTrader, 0);

IF
RegionEnded("TUT_Tutorial_A")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_TUT_LowerDeck_WindegoSpareCharacters(_LeaderTrader)
THEN
NOT DB_TUT_LowerDeck_WindegoSpareCharacters(_LeaderTrader);
//END_REGION

//REGION GREETING_CHANGE
IF
DialogStarted(_Dialog, _Instance)
AND
DB_LLLIB_LeaderTrader_Dialog(_ReferenceName, _Dialog)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Player, _Instance);
LLLIB_LeaderTrader_AdjustGreeting(_Instance, _Player);

PROC
LLLIB_LeaderTrader_AdjustGreeting((INTEGER)_Instance, (CHARACTERGUID)_Player)
AND
DB_LLLIB_LeaderTrader_MetPlayer(_Player)
THEN
DialogSetVariableStringForInstance(_Instance, "LLLIB_Treasure_Greeting_f143b90b-9afc-477e-b133-6a1574341261", "Welcome back! Looking to trade?");

PROC
LLLIB_LeaderTrader_AdjustGreeting((INTEGER)_Instance, (CHARACTERGUID)_Player)
AND
NOT DB_LLLIB_LeaderTrader_MetPlayer(_Player)
THEN
DialogSetVariableStringForInstance(_Instance, "LLLIB_Treasure_Greeting_f143b90b-9afc-477e-b133-6a1574341261", "Hello! Looking to trade?");

IF
DialogEnded(_Dialog, _Instance)
AND
DB_LLLIB_LeaderTrader_Dialog(_ReferenceName, _Dialog)
AND
DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Player, _Instance)
THEN
NOT DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Player, _Instance);

IF
ObjectFlagSet("StartTrade", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Player, _Instance)
THEN
DialogSetVariableStringForInstance(_Instance, "LLLIB_Treasure_Greeting_f143b90b-9afc-477e-b133-6a1574341261", "Anything else?");

IF
RequestTrade(_Player, _LeaderTrader)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Player, _Instance)
THEN
DialogSetVariableStringForInstance(_Instance, "LLLIB_Treasure_Greeting_f143b90b-9afc-477e-b133-6a1574341261", "Anything else?");
//END_REGION

//REGION MEMORY
IF
ObjectFlagSet("LLLIB_Trader_PlayerTalkedToLeaderTrader", (CHARACTERGUID)_Player, _Instance)
THEN
DB_LLLIB_LeaderTrader_MetPlayer(_Player);
//END_REGION

//REGION AVOID_COMBAT
IF
CombatStarted(_CombatID)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
CharacterIsInCombat(_LeaderTrader, 1)
AND
CombatGetIDForCharacter(_LeaderTrader, _CombatID)
AND
ObjectGetFlag(_LeaderTrader, "LLLIB_LeaderTrader_Hiding", 0)
THEN
DB_LLLIB_LeaderTrader_AvoidingCombat(_CombatID);
DisplayText(_LeaderTrader, "I'm retired from the chaos of battle! No more I say!");
CharacterUseSkill(_LeaderTrader, "Shout_ChameleonSkin", _LeaderTrader, 0, 1, 1);
ObjectSetFlag(_LeaderTrader, "LLLIB_LeaderTrader_Hiding", 0);

IF
CharacterStatusApplied(_LeaderTrader, "INVISIBLE", _)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
ObjectGetFlag(_LeaderTrader, "LLLIB_LeaderTrader_Hiding", 1)
THEN
SetOnStage(_LeaderTrader, 0);

IF
CombatEnded(_CombatID)
AND
DB_LLLIB_LeaderTrader_AvoidingCombat(_CombatID)
THEN
TimerLaunch("LLLIB_Timers_LeaderTrader_CombatEnded", 3000);
NOT DB_LLLIB_LeaderTrader_AvoidingCombat(_CombatID);

IF
TimerFinished("LLLIB_Timers_LeaderTrader_CombatEnded")
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
THEN
SetOnStage(_LeaderTrader, 1);
ObjectClearFlag(_LeaderTrader, "LLLIB_LeaderTrader_Hiding");
ObjectSetFlag(_LeaderTrader, "LLLIB_LeaderTrader_WasHiding", 0);
ApplyStatus(_LeaderTrader, "INVISIBLE", 6.0, 1);

IF
CharacterStatusRemoved(_LeaderTrader, "INVISIBLE", _)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
ObjectGetFlag(_LeaderTrader, "LLLIB_LeaderTrader_WasHiding", 1)
THEN
DisplayText(_LeaderTrader, "Hrmph...");
ObjectClearFlag(_LeaderTrader, "LLLIB_LeaderTrader_WasHiding");

IF
ObjectFlagCleared("LLLIB_LeaderTrader_WasHiding", (CHARACTERGUID)_LeaderTrader, _)
AND
DB_LLLIB_LeaderTrader(_LeaderTrader)
AND
DB_LLLIB_Traders_Spawned(_LeaderTrader, _TraderID, _Level)
AND
DB_LLLIB_Traders_Seat(_TraderID, _Level, _Seat, _RequirementID)
THEN
//Reset the chair logic
ClearVarObject(_LeaderTrader, "Seat");
SetVarObject(_LeaderTrader, "Seat", _Seat);
//END_REGION

IF
StoryEvent((CHARACTERGUID)_Trader, "LLLIB_Events_OnLeaderTradeSpawned")
THEN
ApplyStatus(_Trader, "LLLIB_PACIFIST", -1.0, 1);

IF
StoryEvent((CHARACTERGUID)_Trader, "LLLIB_Events_OnLeaderTradeSpawned")
AND
IsGameLevel("TUT_Tutorial_A", 1)
THEN
LLLIB_Helper_UnequipWeapons(_Trader);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
