Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_Mods_Registered(_ModName, _Version, _Author)
//DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
KBSECTION
//REGION REGISTERING
PROC
LeaderUpdater_ClearPreviousVersions((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_Registered(_ModName, _Version, _Author)
THEN
NOT DB_Mods_Registered(_ModName, _Version, _Author);

PROC
LeaderUpdater_RegisterMod((STRING)_ModName, (STRING)_Version, (STRING)_Author)
AND
NOT DB_Mods_Registered(_ModName, _Version, _Author)
THEN
LeaderUpdater_ClearPreviousVersions(_ModName, _Author);
DB_Mods_Registered(_ModName, _Version, _Author);

PROC
LeaderUpdater_RegisterActiveGoal((STRING)_ModName, (STRING)_Author, (STRING)_GoalTitle)
AND
NOT DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
THEN
DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle);

PROC
LeaderUpdater_ClearActiveGoals((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
THEN
NOT DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle);
//END_REGION

//REGION CHECKING
QRY
LeaderUpdater_QRY_ModUpdateNeeded((STRING)_ModName, (STRING)_Version, (STRING)_Author)
AND
NOT DB_Mods_Registered(_ModName, _Version, _Author)
THEN
DB_NOOP(1);

QRY
LeaderUpdater_QRY_ModIsActive((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_Registered(_ModName, _Version, _Author)
AND
DB_Mods_ActiveGoal(_ModName, _Author, _GoalTitle)
AND
SysIsActive(_GoalTitle)
THEN
DB_NOOP(1);
//END_REGION

//REGION UNINSTALLING
PROC
LeaderUpdater_UnregisterMod((STRING)_ModName, (STRING)_Author)
AND
DB_Mods_Registered(_ModName, _Version, _Author)
THEN
NOT DB_Mods_Registered(_ModName, _Version, _Author);

PROC
LeaderUpdater_UninstallMod((STRING)_ModName, (STRING)_Author)
THEN
LeaderUpdater_UnregisterMod(_ModName, _Author);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
