Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_Settings_InitSettings();

//DB_LLLIB_Settings_ItemTemplates(_Name, _ItemTemplate)
//DB_LLLIB_Settings_DialogVariables(_VariableName, _VariableValue)
//DB_LLLIB_Iterator(_Index)

//DB_LLLIB_Settings_Temp_IteratorCreator(_CurrentInt, _MaxInt)
//DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str)
//DB_LLLIB_Settings_Temp_ActiveModsList(_Instance, _Str)
KBSECTION
//REGION SETTINGS
PROC
LLLIB_Settings_InitSettings()
THEN
LLLIB_Settings_InitEquipmentSlots();
LLLIB_Settings_CreateIterator(99);

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_Settings_ItemTemplates", 2);
DB_LLLIB_Settings_ItemTemplates("Book_ModSettings", "LLLIB_BOOK_ModSettingsMenu_646a1194-3383-47fa-a379-ebefa3d2b108");

LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_Settings_DialogVariables", 2);
DB_LLLIB_Settings_DialogVariables("PlayerName", "LLLIB_PlayerName_6c5b9559-53e3-47f7-8881-309a930e4be3");
DB_LLLIB_Settings_DialogVariables("SpeakerName", "LLLIB_SpeakerName_9fc648d4-7984-44e7-afa4-b8a07f21185b");
DB_LLLIB_Settings_DialogVariables("ModSettings_PageInfo", "LLLIB_ModSettings_PageInfo_2b744313-9239-412c-80a8-f3b5e8b56156");
DB_LLLIB_Settings_DialogVariables("RegisteredMods", "LLLIB_Settings_RegisteredMods_50c55fbe-ad1e-490e-92d1-8836ca42ca12");
DB_LLLIB_Settings_DialogVariables("ActiveMods", "LLLIB_Settings_ActiveMods_4099e5fd-da72-464b-9e9e-9bc30a1b994d");

PROC
LLLIB_Settings_InitEquipmentSlots()
AND
NOT DB_LLLIB_EquipmentSlots(_)
THEN
DB_LLLIB_EquipmentSlots("Weapon");
DB_LLLIB_EquipmentSlots("Shield");
DB_LLLIB_EquipmentSlots("Helmet");
DB_LLLIB_EquipmentSlots("Breast");
DB_LLLIB_EquipmentSlots("Leggings");
DB_LLLIB_EquipmentSlots("Ring");
DB_LLLIB_EquipmentSlots("Belt");
DB_LLLIB_EquipmentSlots("Boots");
DB_LLLIB_EquipmentSlots("Gloves");
DB_LLLIB_EquipmentSlots("Amulet");
DB_LLLIB_EquipmentSlots("Ring2");
//END_REGION

//REGION ITERATOR
PROC
LLLIB_Settings_CreateIterator((INTEGER)_MaxInt)
THEN
SysClear("DB_LLLIB_Iterator", 1);
DB_LLLIB_Settings_Temp_IteratorCreator(_MaxInt);
DB_LLLIB_Settings_Temp_StartIteratorCreator(0);

IF
DB_LLLIB_Settings_Temp_StartIteratorCreator((INTEGER)_CurrentInt)
AND
DB_LLLIB_Settings_Temp_IteratorCreator(_MaxInt)
AND
_CurrentInt < _MaxInt
AND
IntegerSum(_CurrentInt, 1, _NextInt)
THEN
DB_LLLIB_Iterator(_CurrentInt);
NOT DB_LLLIB_Settings_Temp_StartIteratorCreator(_CurrentInt);
DB_LLLIB_Settings_Temp_StartIteratorCreator(_NextInt);
//END_REGION

//REGION UPDATING
PROC
LLLIB_System_UpdateDatabases()
THEN
LLLIB_Settings_InitSettings();
LeaderLog_Log("DEBUG", "[LLLIB:Settings] Databases updated.");

PROC
LLLIB_System_ClearDatabases()
THEN
LeaderLog_Log("DEBUG", "[LLLIB:Settings] Databases cleared.");

PROC
LLLIB_Updater_ResetDatabases()
THEN
DB_NOOP(1);
//LeaderLog_Log("DEBUG", "[LLLIB:Settings] Databases cleared and re-initialized.");
//END_REGION

//REGION MOD_DATA
IF
ObjectFlagSet("LLLIB_Settings_ListRegisteredMods", _Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_Settings_ListRegisteredMods")
THEN
LLLIB_Settings_ListRegisteredMods(_Instance);

PROC
LLLIB_Settings_ListRegisteredMods((INTEGER)_Instance)
AND
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _)
THEN
DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, "");

PROC
LLLIB_Settings_ListRegisteredMods((INTEGER)_Instance)
AND
DB_Mods_RegisteredMod(_ModName, _Version, _Author)
AND
DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str)
AND
StringConcatenate("<b>", _ModName, _NameStr1)
AND
StringConcatenate(_NameStr1, "</b>", _NameStr2)
AND
LeaderLog_QRY_Log("COMBINE", _Str, _NameStr2, " (", _Version, ") by ",_Author, "<br>")
AND
DB_LeaderLog_Temp_CombinedString(_CombinedStr)
THEN
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str);
DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _CombinedStr);
NOT DB_LeaderLog_Temp_CombinedString(_CombinedStr);

PROC
LLLIB_Settings_ListRegisteredMods((INTEGER)_Instance)
AND
DB_LLLIB_Settings_DialogVariables("RegisteredMods", _DialogVar)
AND
DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str)
THEN
DialogSetVariableStringForInstance(_Instance, _DialogVar, _Str);
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str);

IF
ObjectFlagSet("LLLIB_Settings_ListActiveMods", _Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_Settings_ListActiveMods")
THEN
LLLIB_Settings_ListActiveMods(_Instance);

PROC
LLLIB_Settings_ListActiveMods((INTEGER)_Instance)
AND
NOT DB_LLLIB_Settings_Temp_ActiveModsList(_Instance, _)
THEN
DB_LLLIB_Settings_Temp_ActiveModsList(_Instance, "");

PROC
LLLIB_Settings_ListActiveMods((INTEGER)_Instance)
AND
DB_Mods_RegisteredMod(_ModName, _Version, _Author)
AND
LeaderUpdater_QRY_ModIsActive(_ModName, _Author)
AND
DB_LLLIB_Settings_Temp_ActiveModsList(_Instance, _Str)
AND
StringConcatenate("<b>", _ModName, _NameStr1)
AND
StringConcatenate(_NameStr1, "</b>", _NameStr2)
AND
LeaderLog_QRY_Log("COMBINE", _Str, _NameStr2, " (", _Version, ") by ",_Author, "<br>")
AND
DB_LeaderLog_Temp_CombinedString(_CombinedStr)
THEN
NOT DB_LLLIB_Settings_Temp_ActiveModsList(_Instance, _Str);
DB_LLLIB_Settings_Temp_ActiveModsList(_Instance, _CombinedStr);
NOT DB_LeaderLog_Temp_CombinedString(_CombinedStr);

PROC
LLLIB_Settings_ListActiveMods((INTEGER)_Instance)
AND
DB_LLLIB_Settings_DialogVariables("ActiveMods", _DialogVar)
AND
DB_LLLIB_Settings_Temp_ActiveModsList(_Instance, _Str)
THEN
DialogSetVariableStringForInstance(_Instance, _DialogVar, _Str);
NOT DB_LLLIB_Settings_Temp_ActiveModsList(_Instance, _Str);
//END_REGION

//REGION SETTINGS_MENU
IF
DialogStarted("LLLIB_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
LeaderLog_SetNextStatusColor(_Player, "#BF00FF");
LeaderLog_SetOneshotTarget(_Player);
LeaderLog_Log("STATUS", "Settings Opened!");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
