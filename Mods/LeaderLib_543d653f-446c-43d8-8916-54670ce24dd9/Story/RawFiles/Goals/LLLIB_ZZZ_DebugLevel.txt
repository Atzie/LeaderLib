Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLLIB_Treasure_Temp_ActiveTraderDialog(_Trader, _Instance)
KBSECTION
IF
GameStarted("LeaderLib_TestLevel",_)
THEN
LLLIB_Treasure_RegisterObject("LLLIB_TestLevel_Trader", CHARACTERGUID_Humans_Male_Clothing_Poor_000_cc53113d-d4f2-4e8f-9ee4-e6aedf51669b);
LLLIB_Treasure_AddTreasureItemTemplate("LLLIB_TestLevel_Trader", "LOOT_Source_Orb_106a7107-cf36-4331-b5b5-6de71969f370", 1, "LLLIB_Debug_TraderRequirements");
LLLIB_Treasure_AddFlagRequirement("LLLIB_Debug_TraderRequirements", "LLLIB_Trader_PlayerTalkedToLeaderTrader", "Object", 1);
//LLLIB_Treasure_AddLevelRequirement("LLLIB_Debug_TraderRequirements", 2, 2);

LLLIB_Treasure_RegisterObject("LLLIB_TestLevel_Chest1", ITEMGUID_LLLIB_Debug_TestChest_ff6c9edf-aa21-447a-8bb2-2a647a8dae46);
LLLIB_Treasure_RegisterTreasureTable("LLLIB_TestLevel_Chest1", "TEST_Generation", "LLLIB_Debug_TraderRequirements", 1);
LLLIB_Treasure_AddTreasureItemTemplate("LLLIB_TestLevel_Chest1", "LOOT_Source_Orb_106a7107-cf36-4331-b5b5-6de71969f370", 2, "LLLIB_TestLevel_Chest1_Requirements");
LLLIB_Treasure_AddFlagRequirement("LLLIB_TestLevel_Chest1_Requirements", "LLLIB_Trader_PlayerTalkedToLeaderTrader", "Object", 0);

//LLLIB_Treasure_AddTreasureItemTemplate("LLLIB_TestLevel_Chest1", "WPN_Dwarves_Sword_2H_B_03f8fb7a-59cf-47eb-9298-79cedaf744e5", 1);
LLLIB_Treasure_Configure_ItemLevel("LLLIB_TestLevel_Chest1", "WPN_Dwarves_Sword_2H_B_03f8fb7a-59cf-47eb-9298-79cedaf744e5", 0, 5, 10);
//LLLIB_Treasure_AddTreasureItemTemplate("LLLIB_TestLevel_Chest1", "WPN_Elves_Spear_2H_B_bec707cd-8340-4d4d-8cdc-edfedf7f311d", 1);
LLLIB_Treasure_Configure_ItemLevel("LLLIB_TestLevel_Chest1", "WPN_Elves_Spear_2H_B_bec707cd-8340-4d4d-8cdc-edfedf7f311d", 0, 1);
LLLIB_Treasure_AddTreasureItemStat("LLLIB_TestLevel_Chest1", "WPN_Cheat_Sword_1H_RuneSlot2", 1);
LLLIB_Treasure_Configure_ItemLevel("LLLIB_TestLevel_Chest1", "WPN_Cheat_Sword_1H_RuneSlot2", 0, 2, 13);
LLLIB_Treasure_Configure_AddDeltaMod("LLLIB_TestLevel_Chest1", "WPN_Cheat_Sword_1H_RuneSlot2", "Boost_Weapon_Ability_FireSpecialist_Staff", 75);
LLLIB_Treasure_Configure_AddDeltaMod("LLLIB_TestLevel_Chest1", "WPN_Cheat_Sword_1H_RuneSlot2", "Boost_Weapon_Secondary_Accuracy_Medium", 20);
LLLIB_Treasure_Configure_AddDeltaMod("LLLIB_TestLevel_Chest1", "WPN_Cheat_Sword_1H_RuneSlot2", "Boost_Weapon_Damage_Air_Medium_Axe", 40);
LLLIB_Treasure_Configure_AddRune("LLLIB_TestLevel_Chest1", "WPN_Cheat_Sword_1H_RuneSlot2", "LOOT_Rune_Flame_Large_9ac874d8-a7e5-48b1-954a-b11b46f6f390", 25);
LLLIB_Treasure_Configure_AddRune("LLLIB_TestLevel_Chest1", "WPN_Cheat_Sword_1H_RuneSlot2", "LOOT_Rune_Masterwork_Large_7e16d9af-6865-4f0f-a67a-144b8ef2da26", 15);
LLLIB_Treasure_Configure_AddRune("LLLIB_TestLevel_Chest1", "WPN_Cheat_Sword_1H_RuneSlot2", "LOOT_Rune_Thunder_Small_f27c8738-e46c-4780-98a1-2db27407e6ea", 15);

IF
GameStarted("LeaderLib_TestLevel",_)
THEN
GlobalSetFlag("LEADERLOG_DEBUG_ENABLED");
TimerLaunch("LeaderLib_TestLevel_IteratorDelay", 500);

IF
TimerFinished("LeaderLib_TestLevel_IteratorDelay")
THEN
LeaderLog_DebugLog("[LLLIB:DebugLevel] ","Searching for players.");
CharacterLaunchIterator("LLLIB_Debug_AddPlayer");

IF
StoryEvent((CHARACTERGUID)_Player, "LLLIB_Debug_AddPlayer")
AND
CharacterIsPlayer(_Player, 1)
AND
NOT DB_IsPlayer(_Player)
THEN
DB_IsPlayer(_Player);
LeaderLog_DebugLog("[LLLIB:DebugLevel] ","Added player to DB_IsPlayer.");
//ObjectClearFlag(_Player, "LLLIB_Trader_PlayerTalkedToLeaderTrader");

IF
DialogStarted(_Dialog, _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, _Trader)
AND
IsTagged(_Trader, "LLLIB_Treasure_GenerateOnDialog", 1)
AND
LLLIB_QRY_Treasure_IsRegisteredObject(_Trader)
THEN
LeaderLog_DebugLog("[LLLIB:DebugLevel] ","Dialog generation tag set, generating items for trader.");

IF
DialogStarted(_Dialog, _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Trader)
AND
IsTagged(_Trader, "LLLIB_Treasure_GenerateOnDialog", 1)
AND
NOT LLLIB_QRY_Treasure_IsRegisteredObject(_Trader)
THEN
LeaderLog_DebugLog("[LLLIB:DebugLevel][ERROR] ","Dialog generation tag set, but trader is not registered!");

IF
CharacterUsedItem(_, ITEMGUID_LLLIB_DebugLever_TestRandom_d3152baa-c6d4-4e8e-a622-a6bb9c149b3e)
AND
LLLIB_Random(1, 19)
AND
DB_LLLIB_Temp_RandomResult(_Ran)
AND
IntegertoString(_Ran, _RanStr)
THEN
NOT DB_LLLIB_Temp_RandomResult(_Ran);
LeaderLog_DebugLog("[LLLIB:TreasureSystem] Random range result: ",_RanStr," | Min: 1 Max: 19");

//REGION LEADER_TRADER
IF
DialogStarted("LLLIB_Trader", _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, _Object)
THEN
DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Object, _Instance);

IF
DialogStarted("LLLIB_Trader", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
ObjectGetFlag(_Player, "LLLIB_Trader_PlayerTalkedToLeaderTrader", 1)
THEN
DialogSetVariableStringForInstance(_Instance, "LLLIB_Treasure_Greeting_f143b90b-9afc-477e-b133-6a1574341261", "Welcome back! Looking to trade?");

IF
DialogEnded("LLLIB_Trader", _Instance)
AND
DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Object, _Instance)
THEN
NOT DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Object, _Instance);

IF
ObjectFlagSet("StartTrade", _Speaker, _Instance)
AND
DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Object, _Instance)
THEN
DialogSetVariableStringForInstance(_Instance, "LLLIB_Treasure_Greeting_f143b90b-9afc-477e-b133-6a1574341261", "Anything else?");

IF
RequestTrade(_Player, _Object)
AND
DB_LLLIB_Debug_Temp_ActiveTraderDialog(_Object, _Instance)
THEN
DialogSetVariableStringForInstance(_Instance, "LLLIB_Treasure_Greeting_f143b90b-9afc-477e-b133-6a1574341261", "Anything else?");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
