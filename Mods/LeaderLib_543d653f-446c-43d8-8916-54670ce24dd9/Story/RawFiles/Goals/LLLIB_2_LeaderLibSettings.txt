Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLLIB_Settings_InitSettings();

//DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str)
KBSECTION
//REGION SETTINGS
PROC
LLLIB_Settings_InitSettings()
THEN
LLLIB_Helper_ClearExistingDatabase("DB_LLLIB_Settings_DialogVariables", 2);
DB_LLLIB_Settings_DialogVariables("RegisteredMods", "LLLIB_Settings_RegisteredMods_50c55fbe-ad1e-490e-92d1-8836ca42ca12");
//END_REGION

//REGION UPDATING
PROC
LLLIB_System_UpdateDatabases()
THEN
LLLIB_Settings_InitSettings();
LeaderLog_Debug("[LLLIB:Settings] Databases updated.");

PROC
LLLIB_System_ClearDatabases()
THEN
SysClear("DB_LLLIB_Settings_MenusPerPage", 1);
SysClear("DB_LLLIB_Settings_RegisteredMenu", 4);
LeaderLog_Debug("[LLLIB:Settings] Databases cleared.");

PROC
LLLIB_Updater_ResetDatabases()
THEN
LLLIB_System_ClearDatabases();
LLLIB_System_UpdateDatabases();
LeaderLog_Debug("[LLLIB:Settings] Databases cleared and re-initialized.");
//END_REGION

IF
ObjectFlagSet("LLLIB_Settings_ListRegisteredMods", (CHARACTERGUID)_Player, _Instance)
AND
LLLIB_Helper_QRY_ClearObjectFlag(_Player, "LLLIB_Settings_ListRegisteredMods")
THEN
LLLIB_Settings_ListRegisteredMods(_Instance);


PROC
LLLIB_Settings_ListRegisteredMods((INTEGER)_Instance)
AND
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _)
THEN
DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, "");

PROC
LLLIB_Settings_ListRegisteredMods((INTEGER)_Instance)
AND
DB_Mods_RegisteredMod(_ModName, _Version, _Author)
AND
DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str)
AND
StringConcatenate("<b>", _ModName, _NameStr1)
AND
StringConcatenate(_NameStr1, "</b>", _NameStr2)
AND
LeaderLog_QRY_Internal_CombineStrings("LLLIB_Settings_ListMods", _Str, _NameStr2, " (", _Version, ") by ",_Author, "<br>")
AND
DB_LeaderLog_Temp_CombinedString("LLLIB_Settings_ListMods", _CombinedStr)
THEN
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str);
DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _CombinedStr);
LeaderLog_ClearCombinedString("LLLIB_Settings_ListMods");

PROC
LLLIB_Settings_ListRegisteredMods((INTEGER)_Instance)
AND
DB_LLLIB_Settings_DialogVariables("RegisteredMods", _DialogVar)
AND
DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str)
THEN
DialogSetVariableStringForInstance(_Instance, _DialogVar, _Str);
NOT DB_LLLIB_Settings_Temp_RegisteredModsList(_Instance, _Str);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader__LeaderLib"
